//===========================================================================
//
//  File Name:    Setup.rul
//
//  Description:  StreamedMP setup main script file
//
//  Comments:     
//
//===========================================================================    

/////////////////////////////////////////////////////////////////////////////
//   INSTALLATION    	 MAINTENANCE           PATCH          RESUME/UPGRADE
//       |                   |                   |                   |
//       '-------------------'---------,---------'-------------------'
//                                     |
//                                 OnBegin()
//                                     |
//       ,-------------------,---------'---------,-------------------,
//       |                   |                   |                   |
// OnFirstUIBefore()  OnMaintUIBefore()  OnPatchUIBefore()  OnResumeUIBefore()
//       |                   |                   |                   |
//      ...                 ...                 ...                 ...
//       |                   |                   |                   |
//  OnFirstUIAfter()  OnMaintUIAfter()    OnPatchUIAfter()   OnResumeUIAfter()
//       |                   |                   |                   |
//       '-------------------'---------,---------'-------------------'
//                                     |
//                                   OnEnd()
//
/////////////////////////////////////////////////////////////////////////////


// Included header files ----------------------------------------------------
#include "ifx.h"

#define SKIN_NAME									"StreamedMP"
#define DEFAULT_SKIN								"DefaultWide" 

#define MEDIAPORTAL_REG_KEY  						"SOFTWARE\\Team MediaPortal\\MediaPortal"
#define MEDIAPORTALUNINSTALL_REG_KEY    			"SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Uninstall\\MediaPortal"
#define STREAMEDMP_REG_KEY  						"SOFTWARE\\StreamedMP"

#define STREAMEDMP_HOMEPAGE_URL						"http://code.google.com/p/streamedmp/" 

// Feature Defines 
#define TVSERIES_LISTICONS_FEATURE					"Main\\tvseriesListIcons"

#define FONTSDEF_FEATURE_OPTIONS					"Main\\fontsDefOptions"
#define FONTSLGE_FEATURE_OPTIONS					"Main\\fontsLgeOptions"

#define NOWPLAYING_DEFAULT							"Main\\nowPlayingDefault"
#define NOWPLAYING_FULLSCREEN_HIDDENMENU            "Main\\nowPlayingFullscreenHiddenMenu"
#define NOWPLAYING_FULLSCREEN_SLIDINGMENU           "Main\\nowPlayingFullscreenSlidingMenu"
#define NOWPLAYING_EDGE_FADE                        "Main\\nowPlayingEdgeFade"
#define NOWPLAYING_EDGE_NOFADE                      "Main\\nowPlayingEdgeNoFade"
#define NOWPLAYING_EDGE_NOMASK                      "Main\\nowPlayingEdgeNoMask"
#define NOWPLAYING_WINDOW_FADE                      "Main\\nowPlayingWindowFade"
#define NOWPLAYING_WINDOW_NOFADE                    "Main\\nowPlayingWindowNoFade"
#define NOWPLAYING_WINDOW_NOMASK                    "Main\\nowPlayingWindowNoMask"

#define TVLOGOS_AUSTRALIA							"TVLogos\\Australia"
#define TVLOGOS_GERMANY_MODERN						"TVLogos\\Germany\\Modern"
#define TVLOGOS_GERMANY_CLASSIC						"TVLogos\\Germany\\Classical"
#define TVLOGOS_GERMANY_TRANSPARENT					"TVLogos\\Germany\\Transparent"
#define TVLOGOS_UK									"TVLogos\\UK"
#define TVLOGOS_CANADA								"TVLogos\\Canada"


// Skin XML defines
#define STREAMEDMP_SKIN_TVSERIES					INSTALLDIR^SKIN_NAME^"TVSeries.xml"
#define STREAMEDMP_SKIN_MOVINGPICTURES				INSTALLDIR^SKIN_NAME^"MovingPictures.xml"
#define STREAMEDMP_SKIN_TVSERIESSETTINGS			INSTALLDIR^SKIN_NAME^"TVSeries.SkinSettings.xml"
#define STREAMEDMP_SKIN_TVMINIGUIDE					INSTALLDIR^SKIN_NAME^"TVMiniGuide.xml"
#define STREAMEDMP_SKIN_MYTVGUIDE					INSTALLDIR^SKIN_NAME^"mytvguide.xml"
#define STREAMEDMP_SKIN_DIALOGTVGUIDE				INSTALLDIR^SKIN_NAME^"dialogTvGuide.xml"
#define STREAMEDMP_SKIN_REFERENCES					INSTALLDIR^SKIN_NAME^"references.xml"

#define STREAMEDMP_SKIN_EMULATORS					INSTALLDIR^SKIN_NAME^"myEmulators.xml"

#define STREAMEDMP_SKIN_4TRACTIVE			        INSTALLDIR^SKIN_NAME^"4TR_Active.xml"
#define STREAMEDMP_SKIN_4TRTVGUIDE			        INSTALLDIR^SKIN_NAME^"4TR_TvGuide.xml"
#define STREAMEDMP_SKIN_4TRPROGRAMINFO		        INSTALLDIR^SKIN_NAME^"4TR_ProgramInfo.xml"
#define STREAMEDMP_SKIN_4TRRECORDEDTV		        INSTALLDIR^SKIN_NAME^"4TR_RecordedTv.xml"
#define STREAMEDMP_SKIN_4TRTVGUIDESEARCH		    INSTALLDIR^SKIN_NAME^"4TR_TvGuideSearch.xml"
#define STREAMEDMP_SKIN_4TRUPCOMING			        INSTALLDIR^SKIN_NAME^"4TR_Upcoming.xml"

#define STREAMEDMP_SKIN_MYTVPROGRAM			        INSTALLDIR^SKIN_NAME^"mytvprogram.xml"
#define STREAMEDMP_SKIN_MYTVRECORDEDINFO	        INSTALLDIR^SKIN_NAME^"mytvRecordedInfo.xml"
#define STREAMEDMP_SKIN_MYTVRECORDEDTV		        INSTALLDIR^SKIN_NAME^"mytvrecordedtv.xml"
#define STREAMEDMP_SKIN_MYTVSCEDULERSERVER	        INSTALLDIR^SKIN_NAME^"mytvschedulerServer.xml"
#define STREAMEDMP_SKIN_MYTVSCEDULERSERVERSEARCH	INSTALLDIR^SKIN_NAME^"mytvschedulerserverSearch.xml"
#define STREAMEDMP_SKIN_MYTVSEARCH					INSTALLDIR^SKIN_NAME^"mytvsearch.xml" 
#define STREAMEDMP_SKIN_VIDEOOSD					INSTALLDIR^SKIN_NAME^"videoOSD.xml"

// Version Defines
#define MEDIAPORTAL_MINNOWARN_VERSION				"1.1.6.27644" 	// 1.2.0 Beta + version check
#define MEDIAPORTAL_MIN_VERSION						"1.1.6.27644"	// 1.2.0 Beta + version check
                           
#define TVSERIES_MIN_VERSION						"3.0.3.1747"
#define MOVINGPICTURES_MIN_VERSION					"1.2.2.1290"
#define FANARTHANDLER_MIN_VERSION					"2.2.4.502"
#define MYLYRICS_MIN_VERSION						"1.6.0.0"
#define GLOBALSEARCH_MIN_VERSION					"1.7.2.0"
#define INFOSERVICE_MIN_VERSION						"1.6.5.210"
#define TRAKT_MIN_VERSION							"1.0.0.1"


// XML Path Defines
#define REFERENCES_LISTALPHA_PATH				"/controls/control[type='listcontrol']/unfocusedAlpha"
#define REFERENCES_PLAYLISTALPHA_PATH			"/controls/control[type='playlistcontrol']/unfocusedAlpha"
#define REFERENCES_THUMBALPHA_PATH				"/controls/control[type='thumbnailpanel']/unfocusedAlpha"

#define REFERENCES_LISTCOLORTEXT_PATH			"/controls/control[type='listcontrol']/textcolor"
#define REFERENCES_LISTCOLORTEXT2_PATH			"/controls/control[type='listcontrol']/textcolor2"
#define REFERENCES_LISTCOLORTEXT3_PATH			"/controls/control[type='listcontrol']/textcolor3"
#define REFERENCES_LISTCOLORWATCHED_PATH		"/controls/control[type='listcontrol']/playedColor"
#define REFERENCES_LISTCOLORREMOTE_PATH			"/controls/control[type='listcontrol']/remoteColor"
#define REFERENCES_PLAYLISTCOLORTEXT_PATH		"/controls/control[type='playlistcontrol']/textcolor"
#define REFERENCES_PLAYLISTCOLORTEXT2_PATH		"/controls/control[type='playlistcontrol']/textcolor2"
#define REFERENCES_PLAYLISTCOLORTEXT3_PATH		"/controls/control[type='playlistcontrol']/textcolor3"
#define REFERENCES_PLAYLISTCOLORWATCHED_PATH	"/controls/control[type='playlistcontrol']/playedColor"
#define REFERENCES_PLAYLISTCOLORREMOTE_PATH		"/controls/control[type='playlistcontrol']/remoteColor"

#define EMULATORS_SCREENSHOT					"/window/controls/control[id='1001']/texture"
    
// TVSeries ImportPaths
#define TVSERIES_FACADE_PATH								"/window/controls/import[@tag='Facade']"
#define TVSERIES_SERIESANDSEASONLISTPOSTERS_PATH			"/window/controls/import[@tag='SeriesAndSeasonListPosters']"
#define TVSERIES_SERIESANDSEASONFILMSTRIP_PATH				"/window/controls/import[@tag='SeriesAndSeasonFilmstrip']"
#define TVSERIES_SERIESLISTBANNERS_PATH						"/window/controls/import[@tag='SeriesListBanners']"
#define TVSERIES_SERIESWIDEBANNERS_PATH						"/window/controls/import[@tag='SeriesWideBanners']"
#define TVSERIES_EPISODELIST_PATH							"/window/controls/import[@tag='EpisodeList']"
#define TVSERIES_GROUPLIST_PATH								"/window/controls/import[@tag='GroupList']"

// MovingPictures ImportPaths
#define MOVPICS_BACKGROUNDOVERLAY_PATH						"/window/controls/import[@tag='BackgroundOverlay']"
#define MOVPICS_LISTVIEW_PATH								"/window/controls/import[@tag='ListView']"
#define MOVPICS_LISTVIEWMEDIAINFO_PATH						"/window/controls/import[@tag='ListViewMediaInfo']"
#define MOVPICS_THUMBVIEW_PATH								"/window/controls/import[@tag='ThumbView']"
#define MOVPICS_THUMBVIEWMEDIAINFO_PATH						"/window/controls/import[@tag='ThumbViewMediaInfo']"
#define MOVPICS_FILMSTRIPVIEW_PATH							"/window/controls/import[@tag='FilmstripView']"
#define MOVPICS_FILMSTRIPMEDIAINFO_PATH						"/window/controls/import[@tag='FilmstripViewMediaInfo']"
#define MOVPICS_FACADE_PATH									"/window/controls/import[@tag='Facade']"
#define MOVPICS_TOPBAR_PATH									"/window/controls/import[@tag='TopBar']"

// TV Mini Guide ImportPaths
#define TVMINIGUIDE_ROWS_PATH								"/window/controls/import[@tag='TVMiniGuide']"

// TV Guide ImportPaths
#define TVGUIDE_ROWS_PATH									"/window/controls/import[@tag='TVGuideChannelTemplate']"

// Dialogs
#define RES_DLG_TVSERIESSELECT						13000
#define RES_DLG_MOVINGPICSSELECT					13001
#define RES_DLG_OPTIONS								13002
#define RES_DLG_TVSERIESADVANCED					13003
#define RES_DLG_MUSICNOWPLAYING						13004

// Dialog Controls
#define RES_RADIO_CHOICE1							1400
#define RES_RADIO_CHOICE2							1401             
#define RES_COMBO_SERIESLAYOUT						1402 
#define RES_BUTTON_TVSERIESADVANCED					1404
#define RES_COMBO_WIDEBANNERLAYOUT					1406	
#define RES_HYPERLINK								1407          
#define RES_COMBO_THUMBLAYOUT						1408

#define RES_CHECK_TOTALEPCOUNT						1304
#define RES_CHECK_LISTIMAGES						1305
  
#define RES_COMBO_FONTS								1401 
#define RES_CHECK_SKINDEFAULT						1402
#define RES_CHECK_USEBASICHOME						1405
#define RES_COMBO_TVGUIDE							1406
#define RES_CHECK_ROUNDEDCOVERS						1408 
#define RES_COMBO_TVMINIGUIDE						1410
#define RES_BUTTON_LISTCOLOURS						1412
#define RES_BUTTON_UNFOCUSEDALPHA					1413
#define RES_CHECK_SHOWHIDDENMENUIMAGE				1414   
#define RES_CHECK_ENABLERANDOMFANARTMYTV    		1416
#define RES_CHECK_ARTWORKICONS						1420

#define RES_BMP_NOWPLAYINGDEFAULT					1400
#define RES_BMP_NOWPLAYINGFULLSCREENHIDDENMENU		1401
#define RES_BMP_NOWPLAYINGEDGEFADE					1402
#define RES_BMP_NOWPLAYINGEDGENOFADE				1403
#define RES_BMP_NOWPLAYINGEDGENOMASK				1404
#define RES_BMP_NOWPLAYINGWINDOWFADE				1405
#define RES_BMP_NOWPLAYINGWINDOWNOFADE				1406
#define RES_BMP_NOWPLAYINGWINDOWNOMASK				1407
#define RES_BMP_NOWPLAYINGFULLSCREENSLIDINGMENU		1408

#define RES_COMBO_NOWPLAYINGCHOICE					1420
        
// Common Dialog Controls
#define RES_PBUT_NEXT         			1   			// ID of Next button
#define RES_PBUT_CLOSE		  			2     			// ID of the (X) button
#define RES_PBUT_CANCEL       			9   			// ID of Cancel button
#define RES_PBUT_BACK        			12   			// ID of Back button

// CSIDL values
#ifndef CSIDL_PROFILE
	#define CSIDL_PROFILE          		0x0028      
#endif

// XML Entries
#define XML_TRAKT_USERNAME				"Username"
#define XML_TRAKT_PASSWORD				"Password"
#define XML_TRAKT_MOVPICS				"MovingPictures"
#define XML_TRAKT_TVSERIES				"TVSeries"
#define XML_TRAKT_MYVIDEOS				"MyVideos"
#define XML_TRAKT_MYFILMS				"MyFilms"
         
// Prototype Functions         
prototype VOID 		CleanCache(STRING);
prototype NUMBER 	GetMediaPortalProgramDir(BYREF STRING);
prototype NUMBER 	GetMediaPortalConfigDir(BYREF STRING); 
prototype NUMBER	GetMediaPortalDirs(STRING, BYREF STRING);
prototype VOID 		SetDefaults();
prototype VOID 		UpdateSettings(); 
prototype NUMBER	GetFileVersion(BYREF STRING, STRING);
prototype VOID		CheckMPVersion();		
prototype NUMBER	CheckPluginVersions(BYREF STRING);
prototype NUMBER	LaunchBrowser(STRING);   
prototype BOOL 		IsMajorUpgrade(BYREF STRING, BYREF STRING, BYREF BOOL);
prototype VOID		MajorUpgradeCleanup();
prototype VOID		GetCachedInstallDir();  
prototype VOID		UninstallCleanup(BOOL, BOOL);       
prototype NUMBER 	SHFolder.SHGetFolderPathA(HWND,NUMBER,NUMBER,NUMBER,BYREF STRING);
prototype VOID		CleanVirtualStoreDir();
prototype NUMBER	GetXMLProperty(STRING,STRING,BYREF STRING);
prototype NUMBER	SetXMLProperty(STRING,STRING,STRING);
prototype VOID		TVSeriesIcons(BOOL);
prototype VOID		EnableControl(STRING, NUMBER, BOOL);
prototype NUMBER	SetMPXMLProperty(STRING, STRING, STRING, STRING);
prototype NUMBER	GetMPXMLProperty(STRING, STRING, STRING ,BYREF STRING); 
prototype NUMBER	SetSkinDefine(STRING,STRING,STRING);
prototype NUMBER	GetSkinDefine(STRING,STRING,BYREF STRING);
prototype BOOL 		GetInstalledVersion(STRING,BYREF STRING);
prototype VOID		SetInstallationDirectories();
prototype BOOL		AdjustForScreenSize();
prototype NUMBER	ReplaceLineInFile(STRING, STRING, STRING);
prototype VOID		ModifySkinFilesForFULLHD(BOOL);
prototype VOID 		ShowNowPlayingPreviewImage(STRING);
prototype VOID 		HideControl(STRING, NUMBER);   
prototype VOID 		ShowControl(STRING, NUMBER);    
prototype VOID		DetectWinLargeFonts();   
prototype STRING	BoolToString(BOOL); 
prototype STRING	BoolToStringInt(BOOL);
//prototype NUMBER	GetVersionDigit(STRING, INT);
//prototype NUMBER 	GetMediaPortalRevision();
prototype VOID		CheckXMLSectionsExist();
prototype NUMBER	CheckStringExists(STRING, STRING);
prototype VOID		ApplyCheckSum();  
prototype VOID		GenerateBasicHome();
prototype VOID		ReadTVLogoDefaults();
prototype VOID		SaveTVLogoSelections();

prototype NUMBER 	ShowSelectTVSeries(BOOL);
prototype NUMBER 	ShowSelectMovingPics(BOOL);
prototype NUMBER 	ShowSelectOptions(BOOL);
prototype NUMBER 	ShowTVSeriesAdvanced(BOOL); 
prototype NUMBER	ShowSelectNowPlaying(BOOL);
 
prototype VOID		GetTraktSettings();
prototype VOID		SetTraktSettings();

// Global Variables
BOOL g_bTVSeriesDefault;
BOOL g_bMovingPicturesDefault;
BOOL g_bMajorUpgrade;
BOOL g_bSetAsDefaultSkin;
BOOL g_bShowTotalEpisodeCount;
BOOL g_bShowArtworkIcons;
BOOL g_bShowIconsInListView;
BOOL g_bStartBasicHome; 
BOOL g_bShowRoundedCovers;
BOOL g_bShowHiddenMenuImage;      
BOOL g_bEnableRandomFanartInMyTV;
BOOL g_bBasicHomeUpgrade;
BOOL g_bLargeFonts;                         
                               
STRING g_sMajorUpgradeGUID;
STRING g_sTVGuideSize;
STRING g_sTVMiniGuideSize; 
STRING g_sDownloadInstallDir;   
STRING g_sWideBannerLayout;
STRING g_sUnfocusedAlphaListItems;
STRING g_sUnfocusedAlphaThumbs;  
STRING g_sNowPlayingChoice; 
STRING g_sThumbLayout;
  
STRING g_sMediaPortalProgramDir;
STRING g_sLanguageDir;
STRING g_sThumbsDir;
STRING g_sTVLogosDir;
STRING g_sCacheDir; 
STRING g_sPluginsDir;
STRING g_sConfigDir;    
STRING g_sFanartDir;    
STRING g_sDatabaseDir;
STRING g_sClearArtDir;

// Colour Variables
STRING g_sTextColor;
STRING g_sText2Color;
STRING g_sText3Color;
STRING g_sWatchedColor;
STRING g_sRemoteColor;

STRING g_sTVLogosAustralia;
STRING g_sTVLogosGermanyClassical;
STRING g_sTVLogosGermanyModern;
STRING g_sTVLogosGermanyTransparent;
STRING g_sTVLogosCanada;
STRING g_sTVLogosUK;
       
STRING g_sTraktUserName;
STRING g_sTraktPassword;
STRING g_sTraktTVSeries;
STRING g_sTraktMovingPictures;
STRING g_sTraktMyVideos;
STRING g_sTraktMyFilms;

//---------------------------------------------------------------------------
// OnBegin
//
// The OnBegin event is called directly by the framework after the setup
// initializes.
//---------------------------------------------------------------------------
function OnBegin()
	STRING szVersion, szUpgradeMessage, szValue;
	NUMBER nResult;
	BOOL bUpgradeSupported;	
begin 
    
	DetectWinLargeFonts();
 
	// See if older version is installed
	g_bMajorUpgrade = IsMajorUpgrade(szVersion, g_sMajorUpgradeGUID, bUpgradeSupported);
	if g_bMajorUpgrade then
		// check if version supports upgrades
		if bUpgradeSupported then
			if (MODE!=SILENTMODE) then
				szUpgradeMessage = "Setup is about to perform a Major Upgrade from: v" + szVersion + "\nThe previous installation will " +
								   "be removed, Do you wish to continue?";
				nResult = SprintfBox( YES|NO|CANCEL,"Major Upgrade",szUpgradeMessage, INFORMATION);			
				if (nResult != IDYES) then
					abort;
				endif;				
			endif;
		else
			if (MODE!=SILENTMODE) then	
			 	szUpgradeMessage = "Setup cannot perform an Upgrade from: v" + szVersion + "\nPlease uninstall from the Add\\Remove programs " +
								   "and run setup again.";
				MessageBox(szUpgradeMessage,INFORMATION);				
			endif;
			abort;
		endif;
	endif; 		
	                                                              
	SetDefaults();		
end;           


/// *******************************************************
/// Sets the Installation Directories used by MediaPortal
/// Reads MediaPortalDirs.xml and if fails returns defaults
///
function void SetInstallationDirectories()
	STRING svValue;	
begin
	// Set the installation directory to one currently used by MediaPortal
	if (GetMediaPortalProgramDir(g_sMediaPortalProgramDir) < 0) then
		g_sMediaPortalProgramDir = ProgramFilesFolder^"Team MediaPortal\\MediaPortal";	
	endif;	
	
	// Get any custom defined paths from MediaPortalDirs.xml	
	if GetMediaPortalDirs("Skin",svValue) < 0 then
		svValue = CommonAppDataFolder^"Team MediaPortal\\MediaPortal\\skin";
	endif;
	INSTALLDIR = svValue;
	
	if GetMediaPortalDirs("Plugins",g_sPluginsDir) < 0 then
		g_sPluginsDir = g_sMediaPortalProgramDir^"plugins";
	endif;               
	if GetMediaPortalDirs("Config",g_sConfigDir) < 0 then
		if GetMediaPortalConfigDir(g_sConfigDir) < 0 then
		    g_sConfigDir = CommonAppDataFolder^"Team MediaPortal\\MediaPortal";
		endif;		
	endif;               
	if GetMediaPortalDirs("Language",g_sLanguageDir) < 0 then
		if GetMediaPortalConfigDir(svValue) < 0 then
			svValue = CommonAppDataFolder^"Team MediaPortal\\MediaPortal";			
		endif;
		// Append Language to program data dir
		g_sLanguageDir = svValue^"language";
	endif;                                              	           	
	if GetMediaPortalDirs("Thumbs",g_sThumbsDir) < 0 then
		if GetMediaPortalConfigDir(svValue) < 0 then
	    	svValue = CommonAppDataFolder^"Team MediaPortal\\MediaPortal";
	    endif;
	    // Append thumbs to program data dir    
		g_sThumbsDir = svValue^"thumbs";
	endif;
	g_sTVLogosDir = g_sThumbsDir^"tv\\logos";    
	g_sFanartDir = g_sThumbsDir^"Skin Fanart";
	g_sClearArtDir = g_sThumbsDir^"ClearArt";
	
	if GetMediaPortalDirs("Cache",g_sCacheDir) < 0 then
		if GetMediaPortalConfigDir(svValue) < 0 then;
	    	svValue = CommonAppDataFolder^"Team MediaPortal\\MediaPortal";
	    endif;
		g_sCacheDir = svValue^"Cache";
	endif;  
	
	if GetMediaPortalDirs("Database",g_sDatabaseDir) < 0 then
		if GetMediaPortalConfigDir(svValue) < 0 then;
	    	svValue = CommonAppDataFolder^"Team MediaPortal\\MediaPortal";
	    endif;
		g_sDatabaseDir = svValue^"Database";
	endif;
   	
   	// Set MSI Dirs as properties   	
   	MsiSetProperty(ISMSI_HANDLE,"MPTVLOGOSDIR",g_sTVLogosDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPLANGUAGESDIR",g_sLanguageDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPPLUGINSDIR",g_sPluginsDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPCONFIGDIR",g_sConfigDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPBINDIR",g_sMediaPortalProgramDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPFANARTDIR",g_sFanartDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPCLEARARTDIR",g_sClearArtDir);
   	MsiSetProperty(ISMSI_HANDLE,"MPDATABASEDIR",g_sDatabaseDir);
   	   	
	// Set Installation Directories used by MSI	
	FeatureSetTarget(MEDIA,"<MPTVLOGOSDIR>",g_sTVLogosDir);
	FeatureSetTarget(MEDIA,"<MPLANGUAGESDIR>",g_sLanguageDir);
	FeatureSetTarget(MEDIA,"<MPPLUGINSDIR>",g_sPluginsDir);
	FeatureSetTarget(MEDIA,"<MPCONFIGDIR>",g_sConfigDir);
	FeatureSetTarget(MEDIA,"<MPBINDIR>",g_sMediaPortalProgramDir);
	FeatureSetTarget(MEDIA,"<MPFANARTDIR>",g_sFanartDir);
	FeatureSetTarget(MEDIA,"<MPCLEARARTDIR>",g_sClearArtDir);
	FeatureSetTarget(MEDIA,"<MPDATABASEDIR>",g_sDatabaseDir);

end;

function VOID SetDefaults()
	STRING svValue, svString, sFile;
	NUMBER nvType, nvSize;
begin 
	
	// Set Installation Directories
	SetInstallationDirectories();

	// Read TVSeries Default 
	sFile = g_sConfigDir^"StreamedMPConfig.xml";
	if GetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesDefaultStyle", svValue) < 0 then
		svValue = "1";
	endif;
	if svValue = "1" then
		g_bTVSeriesDefault = TRUE;			
	endif;	
	
	// Read TVSeries widebanner layout	
	if GetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesWideBannerMod", g_sWideBannerLayout) < 0 then
		g_sWideBannerLayout = "0";
	endif;
	 
	// Determine if 'Total Episode Count' fields are added to List View 	
	if GetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesShowTotalEpisodeCounts", svValue) < 0 then
		svValue = "0";
	endif;
	if svValue = "1" then
		g_bShowTotalEpisodeCount = TRUE;			
	endif;	
		
	// Determine if 'Watched/Unwatched' Icons are added to List View	
	if GetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesShowIconsInListView", svValue) < 0 then
		svValue = "1";
	endif;
	if svValue = "1" then
		g_bShowIconsInListView = TRUE;			
	endif;	
	
	// Read Moving Pictures Default	
	if GetMPXMLProperty(sFile, "MovingPicturesConfig", "movingpicturesDefaultStyle", svValue) < 0 then
		svValue = "1";
	endif;
	if svValue = "1" then
		g_bMovingPicturesDefault = TRUE;			
	endif;	
	
	// Read MovingPictures Thumb layout	
	if GetMPXMLProperty(sFile, "MovingPicturesConfig", "movingpicturesThumbMod", g_sThumbLayout) < 0 then
		g_sThumbLayout = "0";
	endif;
	
	// Read TVGuide Size default
	if GetMPXMLProperty(sFile, "TVConfig", "tvGuideSize", g_sTVGuideSize) < 0 then
		g_sTVGuideSize = "10";
	endif;
	
	// Read TVMiniGuide Size default
	if GetMPXMLProperty(sFile, "TVConfig", "tvMiniGuideSize", g_sTVMiniGuideSize) < 0 then
		g_sTVMiniGuideSize = "7";
	endif;
	
	// Enable Random Fanart in MyTV
	if GetMPXMLProperty(sFile, "TVConfig", "tvEnableRandomTVSeriesFanart", svValue) < 0 then
		svValue = "0";
	endif;
	if svValue = "1" then
		g_bEnableRandomFanartInMyTV = TRUE;
	endif;        
    
	// Read NowPlaying Style default
	if GetMPXMLProperty(sFile, "MusicConfig", "musicNowPlayingStyle", g_sNowPlayingChoice) < 0 then
		g_sNowPlayingChoice = "0";
	endif;
     	                        
	// Read Font Size default 
	if GetMPXMLProperty(sFile, "MiscConfig", "miscUseLargeFonts", svValue) < 0 then
		svValue = "0";
	endif;
	if svValue = "1" then
		g_bLargeFonts = TRUE;
	endif;
		
	// Read Show Hidden Menu Image
	if GetMPXMLProperty(sFile, "MiscConfig", "miscShowHiddenMenuImage", svValue) < 0 then
		svValue = "1";
	endif;
	if svValue = "1" then
		g_bShowHiddenMenuImage = TRUE;
	endif;
	
	// Read Icons in Artwork default
	if GetMPXMLProperty(sFile, "MiscConfig", "miscShowIconsInArtwork", svValue) < 0 then
		svValue = "1";
	endif;
	if svValue = "1" then
		g_bShowArtworkIcons = TRUE;
	endif;
	
	// Rounded Covers in Fanart Style (TVSeries/MovingPictures) default
	if GetMPXMLProperty(sFile, "MiscConfig", "miscShowRoundedCovers", svValue) < 0 then
		svValue = "1";
	endif;
	if svValue = "1" then
		g_bShowRoundedCovers = TRUE;
	endif;
	
	// Unfocused Alpha List Layout
	if GetMPXMLProperty(sFile, "MiscConfig", "miscUnfocusedAlphaListItems", g_sUnfocusedAlphaListItems) < 0 then
		g_sUnfocusedAlphaListItems = "100";
	endif;
	               	
	// Unfocused Alpha Thumbs Layout
	if GetMPXMLProperty(sFile, "MiscConfig", "miscUnfocusedAlphaThumbs", g_sUnfocusedAlphaThumbs) < 0 then
		g_sUnfocusedAlphaThumbs = "100";
	endif;
		
	// Get List Facade Colors
	if GetMPXMLProperty(sFile, "MiscConfig", "miscTextColor", g_sTextColor) < 0 then
		g_sTextColor = "FFFFFF";
	endif;
	if GetMPXMLProperty(sFile, "MiscConfig", "miscTextColor2", g_sText2Color) < 0 then
		g_sText2Color = "FFFFFF";
	endif;
	if GetMPXMLProperty(sFile, "MiscConfig", "miscTextColor3", g_sText3Color) < 0 then
		g_sText3Color = "FFFFFF";
	endif;
	if GetMPXMLProperty(sFile, "MiscConfig", "miscWatchedColor", g_sWatchedColor) < 0 then
		g_sWatchedColor = "808080";
	endif;
	if GetMPXMLProperty(sFile, "MiscConfig", "miscRemoteColor", g_sRemoteColor) < 0 then
		g_sRemoteColor = "FFA075";
	endif;
			
	// Get Start With Basic Home property
	sFile = g_sConfigDir^"MediaPortal.xml";
	if GetMPXMLProperty(sFile, "gui","startbasichome", svValue) < 0 then
		g_bStartBasicHome = TRUE;
	else
		if svValue = "yes" then
			g_bStartBasicHome = TRUE;
		endif;
	endif;	
	
	// Trakt Migration, for old users
	// Set Trakt Defaults
	// Settings will be read from config file later
	g_sTraktUserName = "";
	g_sTraktPassword = "";
	g_sTraktMyVideos = "-1";
	g_sTraktMyFilms = "-1";
	g_sTraktTVSeries = "0";
	if (Is(FILE_EXISTS, g_sPluginsDir ^ "Windows\\MovingPictures.dll")) then
		g_sTraktMovingPictures = "1";
	else
		g_sTraktMovingPictures = "-1";
	endif;
	
	GetTraktSettings();
	
	g_bSetAsDefaultSkin = TRUE; // Should always be default	
	
end;

function BOOL IsMajorUpgrade(sMajorUpgradeVersion, sMajorUpgradeGUID, bUpgradeSupported)
	STRING szKey;
	NUMBER nvBufferSize, nResult;
	BOOL bUpgrade;
begin
    nvBufferSize = 256;
    bUpgrade = TRUE;
    
    // Major Upgrades requires a change to the ProductCode GUID and Version every release
    // Don't change the UpgradeCode
    
    // Get Properties for MajorUpgrade
    // Returns the GUID in svMajorUpgrade of previous install             
 	MsiGetProperty(ISMSI_HANDLE,"MAJORUPGRADE_01",sMajorUpgradeGUID,nvBufferSize);
 	if StrLength(sMajorUpgradeGUID) = 0 then
 		return FALSE;
 	endif;
 	
 	// Check if there is any versions we dont want to support upgrades on		
	if sMajorUpgradeGUID = "{7A7760CC-256A-4ECD-BE3F-E7644FAD5DA8}" then
		
		sMajorUpgradeVersion = "0.1.167";
		bUpgradeSupported = FALSE;		
		return TRUE;
	
	elseif sMajorUpgradeGUID = "{437C996D-2A3D-4809-89F7-E25A66A463B1}" then
		sMajorUpgradeVersion = "0.2.215";
		bUpgradeSupported = FALSE;		
		return TRUE;
	
	elseif sMajorUpgradeGUID = "{E8B2D59B-84CD-4817-961B-BEA89324BAB3}" then
		sMajorUpgradeVersion = "0.2.219";
		bUpgradeSupported = TRUE;		
		return TRUE;
	
	else
		// Return the previous product version if GUID matches
	 	bUpgradeSupported = GetInstalledVersion(sMajorUpgradeGUID,sMajorUpgradeVersion);
		return bUpgradeSupported;
			
	endif;   
		
 	return FALSE;
end;

function BOOL GetInstalledVersion(sGUID, sMajorUpgradeVersion)
	NUMBER nvType, nvSize;
	STRING svValue;
begin
    
    RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
        
    if RegDBKeyExist(STREAMEDMP_REG_KEY) < 0 then
    	return FALSE;
    endif;
    
    if RegDBGetKeyValueEx(STREAMEDMP_REG_KEY,"PRODUCTGUID",nvType,svValue,nvSize) < 0 then
    	return FALSE;
    else
        if svValue != sGUID then
        	return FALSE;
        endif;
        
        RegDBGetKeyValueEx(STREAMEDMP_REG_KEY,"VERSION",nvType,sMajorUpgradeVersion,nvSize);
    endif;
    
    return TRUE;
end;
    
//---------------------------------------------------------------------------
// OnFirstUIBefore
//
// The OnFirstUIBefore event is called by the framework when the setup is
// running in first install mode. By default this event displays UI allowing
// the end user to specify installation parameters.
//---------------------------------------------------------------------------
function OnFirstUIBefore()
    NUMBER nResult, nSetupType, nvSize, nUser;
    STRING szTitle, szMsg, szQuestion, svName, svCompany, szFile;
    STRING svMPVersion, svValue;    
	BOOL bCustom, bTVLogosRead;	
begin

	SHELL_OBJECT_FOLDER = @PRODUCT_NAME;	   
    
	nSetupType = TYPICAL;	
    
    // Check that MediaPortal is installed, also check version
    CheckMPVersion();  
    
    // UnCheck TV-Logos, user only needs one set
    FeatureSelectItem ( MEDIA, "TVLogos", FALSE );
    
Dlg_SdWelcome:
    szTitle = "";
    szMsg   = @WELCOME;
    if (MODE != SILENTMODE) then
    	nResult = SdWelcome(szTitle, szMsg);
    	if (nResult = BACK) goto Dlg_SdWelcome;
    endif;
	
	szTitle   = "";
	svName    = "";
    svCompany = "";
   
Dlg_SdFeatureTree: 
    szTitle    = "";
    szMsg      = "";
    
    // Read TV Logo defaults
    if bTVLogosRead = FALSE then
    	ReadTVLogoDefaults();
    endif;
    bTVLogosRead = TRUE;
    
    if (nSetupType = TYPICAL) then
    	if (MODE != SILENTMODE) then
			nResult = SdFeatureTree(szTitle, szMsg, INSTALLDIR, "", 2);
			if (nResult = BACK) goto Dlg_SdWelcome;  
		endif;
    endif;
    // Save TV Logo Selections
    SaveTVLogoSelections();
         
Dlg_SelectTVSeries:
	nResult = ShowSelectTVSeries(FALSE);
	if (nResult = BACK) goto Dlg_SdFeatureTree;  

Dlg_SelectMovingPictures:
	nResult = ShowSelectMovingPics(FALSE);
	if (nResult = BACK) goto Dlg_SelectTVSeries;  

Dlg_SelectNowPlaying:
	nResult = ShowSelectNowPlaying(FALSE);
	if (nResult = BACK) goto Dlg_SelectMovingPictures;
	
Dlg_SelectOptions:
	nResult = ShowSelectOptions(FALSE);
	if (nResult = BACK) goto Dlg_SelectNowPlaying;  
	
Dlg_SdStartCopy:
    szTitle = "";
    szMsg   = "";
    if (MODE != SILENTMODE) then      
    	nResult = SdStartCopy2( szTitle, szMsg );
    else
    	nResult = NEXT;
    endif;
	
	if (nResult = BACK) then
    	goto Dlg_SelectOptions;
    else
    	// Confirm Plugin versions are up to date if chosen not to install them    	   
    	if (MODE != SILENTMODE) then
		    szQuestion = "";
		    nResult = CheckPluginVersions(szQuestion);
	 		if nResult < 0  then 			
	 			if (AskYesNo ( szQuestion, YES ) = YES) goto Dlg_SdFeatureTree; 	
	 		endif;                        
 		endif;
    endif;
    
    /*if g_bMajorUpgrade then
    	// Remove old Editor backups (these are no longer compatible)
	    if GetMediaPortalConfigDir(svValue) != -1 then;
	    	DeleteFile(svValue^"skin"^SKIN_NAME^"Basichome.xml.backup.*");
	    endif;
    endif;*/        
    
    // setup default status
    Enable(STATUSEX);
    
    // Cleanup Virutal Store
    CleanVirtualStoreDir();
 
    return 0;
end;
//---------------------------------------------------------------------------
// OnMaintUIBefore
//
// The OnMaintUIBefore event is called by the framework when the setup is
// running in maintenance mode. By default this event displays UI that
// allows the end user to add or remove features, repair currently
// installed features or uninstall the application.
//---------------------------------------------------------------------------
function OnMaintUIBefore()
	NUMBER nResult, nType;
	STRING szTitle, szMsg, svDir, svResult, szCaption;
begin				
	 
Dlg_Start:

    // Added in Version 9.5 - Support for REMOVEONLY option.
    if( !REMOVEONLY ) then
		// In standard mode show maintenance dialog
		Disable(BACKBUTTON);
		if (MODE != SILENTMODE) then
			nType = SdWelcomeMaint(szTitle, szMsg, MODIFY);
		else
			// for silent maint just do Modify
			nType = MODIFY;
		endif;
		Enable(BACKBUTTON);
	else
        // Hide the initial progress dialog as otherwise the user can
        // click on it, and hide the MessageBox.
        Disable( DIALOGCACHE );

        // In RemoveOnly mode, set to remove.
        nType = REMOVEALL;
    endif;
	
	// Show Uninstall Confirmation Dialog
    if ( nType = REMOVEALL ) then
    	if (MODE != SILENTMODE) then
			nResult = MessageBox( SdLoadString( IFX_MAINTUI_MSG ), MB_YESNO );
			if (nResult != IDYES ) then
	            
	            if( REMOVEONLY ) then
	                // In REMOVEONLY mode, abort the setup.
	                abort;
	            else
	                // In non-REMOVEONLY mode, redisplay the previous dialog.
				    goto Dlg_Start;
	            endif;
	
			endif;
		endif;
	endif;

	nResult = NEXT;

Dlg_SdFeatureTree:
	if (nType = MODIFY) then
		szTitle = "";
		szMsg = "";
		if (MODE != SILENTMODE) then		
			nResult = SdFeatureTree(szTitle, szMsg, INSTALLDIR, "", 2);
			if (nResult = BACK) goto Dlg_Start;
		else
			ReadTVLogoDefaults();
		endif;
		SaveTVLogoSelections();
	endif;


Dlg_SelectTVSeries:
	if (nType = MODIFY) then
		nResult = ShowSelectTVSeries(FALSE);
		if (nResult = BACK) goto Dlg_SdFeatureTree;  
	endif;
	
Dlg_SelectMovingPictures:
	if (nType = MODIFY) then
		nResult = ShowSelectMovingPics(FALSE);
		if (nResult = BACK) goto Dlg_SelectTVSeries; 
	endif;  
	
Dlg_SelectNowPlaying:
	if (nType = MODIFY) then
		nResult = ShowSelectNowPlaying(FALSE);
		if (nResult = BACK) goto Dlg_SelectMovingPictures; 
	endif;

Dlg_SelectOptions:
	if (nType = MODIFY) then
		nResult = ShowSelectOptions(FALSE);
		if (nResult = BACK) goto Dlg_SelectNowPlaying; 
	endif;
		
	switch(nType)
		case REMOVEALL: 
			FeatureRemoveAll();
			GetCachedInstallDir();
		case REPAIR:    FeatureReinstall();
		case MODIFY:	UpdateSettings();
	endswitch;
    
	// setup default status 
	SetStatusWindow(0, "");
	Enable(STATUSEX); 	
	StatusUpdate(ON, 100);
	
	if (nType = MODIFY) then			
	    // Cleanup Virutal Store
	    CleanVirtualStoreDir();
	endif;
	
end;
//---------------------------------------------------------------------------
// OnFirstUIAfter
//
// The OnFirstUIAfter event called by the framework after the file transfer
// of the setup when the setup is running in first install mode. By default
// this event displays UI that informs the end user that the setup has been
// completed successfully.
//---------------------------------------------------------------------------
function OnFirstUIAfter()
    STRING szTitle, szMsg1, szMsg2, szOption1, szOption2, szCmdLine, szSite, szFile;
    NUMBER bOpt1, bOpt2;
begin
	
	// Delete old Trakt Process plugin if it still exists
	if (Is(FILE_EXISTS, g_sPluginsDir ^ "windows\\TraktPlugin.dll")) then
		if (Is(FILE_EXISTS, g_sPluginsDir ^ "process\\TraktPlugin.dll")) then
			DeleteFile(g_sPluginsDir ^ "process\\TraktPlugin.dll");
		endif;
	endif;
	
 	// Update Settings
	UpdateSettings();
	
	// Generate new BasicHome screen
	GenerateBasicHome();
	
	// Clean Cache Folder    
    CleanCache("Default");
    CleanCache("DefaultWide");
	CleanCache(SKIN_NAME);
	        
    // Major Upgrade Cleanup
    if g_bMajorUpgrade then
    	MajorUpgradeCleanup();
    endif;
    
	Disable(STATUSEX);
 	
 	if (MODE != SILENTMODE) then
	 	szFile = g_sMediaPortalProgramDir ^ "SMPEditor.exe";
	 	
		bOpt1  = FALSE;
		bOpt2  = FALSE;
		szMsg1 = SdLoadString(IFX_SDFINISH_MSG1);
		szOption1 = "&Launch Home Page in default Browser"; 
		
		if Is(FILE_EXISTS,szFile) && g_bStartBasicHome then
			szOption2 = "Launch &StreamedMP Basic Home Editor";
		endif;
		SdFinishEx(szTitle, szMsg1, szMsg2, szOption1, szOption2, bOpt1, bOpt2);	
		
		// If reboot not needed and option selected
		if (!BATCH_INSTALL && bOpt1) then
			// Launch webpage
			szSite = STREAMEDMP_HOMEPAGE_URL;
			LaunchBrowser(szSite);
		endif;
		if (!BATCH_INSTALL && bOpt2) then
			LaunchApp(szFile, szCmdLine);
		endif;
	 endif;
	
end;
//---------------------------------------------------------------------------
// OnMaintUIAfter
//
// The OnMaintUIAfter event called by the framework after the file transfer
// of the setup when the setup is running in maintenance mode. By default
// this event displays UI that informs the end user that the maintenance setup
// has been completed successfully.
//---------------------------------------------------------------------------
function OnMaintUIAfter()
	STRING szTitle, szMsg1, szMsg2, szOption1, szOption2;
	NUMBER bOpt1, bOpt2;
begin     

    // Clean Cache Folder    
    CleanCache("Default");
    CleanCache("DefaultWide");
    CleanCache(SKIN_NAME);
    
	Disable(STATUSEX);	
    
    if (MODE != SILENTMODE) then
	    bOpt1   = FALSE;
		bOpt2   = FALSE;
    	
    	// Uninstall.
	    if( REMOVEALLMODE ) then
	        szTitle = SdLoadString(IFX_SDFINISH_REMOVE_TITLE);
	        szMsg1 = SdLoadString(IFX_SDFINISH_REMOVE_MSG1);
	        
	        bOpt1 = FALSE;
	        bOpt2 = FALSE;
	        szOption1 = "Clear Skin configuration settings (restore defaults)?";
	        szOption2 = "Clear BasicHome configuration settings and backups?";      
	    else
	        szTitle = SdLoadString(IFX_SDFINISH_MAINT_TITLE);    
	        szMsg1  = SdLoadString(IFX_SDFINISH_MAINT_MSG1);
	    endif;
	
		SdFinishEx(szTitle, szMsg1, szMsg2, szOption1, szOption2, bOpt1, bOpt2);
	endif;
	
    if ( REMOVEALLMODE ) then
    	UninstallCleanup(bOpt1, bOpt2);
    endif;
    
end; 

function VOID CleanCache(sSkin)
	STRING svValue;	
begin
    
    // Delete Skin Cache Folder
    SetStatusWindow(100, "Removing " + sSkin + " from MediaPortal cache.");
    Delay(1);
    svValue = g_sCacheDir ^ sSkin;
    DeleteDir(svValue,ALLCONTENTS);
    
end;      

function NUMBER GetMediaPortalProgramDir(svValue)	
	NUMBER nvSize,nvType;
begin

   	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	
	// Get MediaPortal Install Directory from Uninstall path in registry
	// This should exist if MediaPortal is correctly installed
	if (RegDBGetKeyValueEx(MEDIAPORTALUNINSTALL_REG_KEY,"InstallPath",nvType,svValue,nvSize) < 0) then
        // Fallback to this directory incase its a development build
        // Note: This Key is no longer created in MediaPortal 1.1.0+
		if (RegDBGetKeyValueEx(MEDIAPORTAL_REG_KEY,"ApplicationDir",nvType,svValue,nvSize) < 0) then
			return -1;	
		endif;
		
	endif;
		
	return 0;
end;

function NUMBER GetMediaPortalConfigDir(svValue)
	NUMBER nvSize,nvType;
begin    
	// Fallback incase MediaPortalDirs.xml fails us  
	// Note: This key no longer creted in MediaPortal 1.1.0+
   	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	
	// Note: This Key is no longer created in MediaPortal 1.1.0+	
	if (RegDBGetKeyValueEx(MEDIAPORTAL_REG_KEY,"ConfigDir",nvType,svValue,nvSize) < 0) then
		return -1;	
	endif;
	
	return 0;
end; 

function NUMBER GetMediaPortalDirs(sType, sValue)
	OBJECT oXMLdoc, oNodeList, oDirID;
	STRING sNode, sPath, sDisk, sFileName;
	INT i;
begin                        
    
    try 
	   	set oXMLdoc = CreateObject("Microsoft.XMLDOM");
		    
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
	    	    
	    // MediaPortalDirs.xml contains a reference to all Paths used by MediaPortal
	    // Currently this file lives in the main program files directory
	    // But we should first check the personal directory as user can override there as well	   
	    sFileName = PersonalFolder ^ "Team MediaPortal\\MediaPortalDirs.xml";
	    if !Is(FILE_EXISTS,sFileName) then
	        
	        // Check Default location
		    sFileName = g_sMediaPortalProgramDir^"MediaPortalDirs.xml";
		    if !Is(FILE_EXISTS,sFileName) then
		    	// If the file does not exist check the program data directory
		    	sFileName = CommonAppDataFolder ^ "Team MediaPortal\\MediaPortal\\MediaPortalDirs.xml";
		    endif;   
		
		endif;
		// Load the document	
		oXMLdoc.Load (sFileName);	
		
	    sNode = "/Config/Dir";	    
	    set oNodeList = oXMLdoc.DocumentElement.SelectNodes(sNode); 	    	    
	    
	    // Examine all <Dir> nodes to find id
	    for i=0 to oNodeList.length - 1
		 	
		 	// The id attribute holds the path identifier e.g. Config, Plugins, Skin
		 	set oDirID = oNodeList.Item(i).Attributes.GetNamedItem("id");
		 	
		 	// Get Path Property value
		 	if oDirID.Text = sType then
		 		sPath = oNodeList.Item(i).SelectSingleNode("Path").Text;		 		
		 	endif;
		
		endfor;
		
		// Replace path variables
		StrToLower(sPath,sPath);
		StrReplace(sPath,"%programdata%\\","%programdata%",0);
		StrReplace(sPath,"%programdata%",CommonAppDataFolder,0);
		
		// Check if Path is valid, otherwise append MediaPortal root directory
		if (GetDisk(sPath, sDisk) < 0) then
		     sPath = g_sMediaPortalProgramDir^sPath;
		endif;
		
		StrAddLastSlash(sPath);
		
		// Return value
		sValue = sPath;
		    
	    // release objects used
	    set oXMLdoc = NOTHING;	    
	    set oNodeList = NOTHING;
	    set oDirID = NOTHING;	    	    
	 	   
	catch                  
				
	    return -1;    
	    
	endcatch;	       

    Enable(LOGGING);         
    
    if !Is(VALID_PATH,sValue) then
    	return -1;
    endif;
    
    return 0;
    	 	
end;

function number ShowSelectTVSeries(bModify)
	STRING szDialogName, szErrorMsg, szUrl;
	NUMBER nCmdValue;
	BOOL bDone, bValidate; 
	LIST listWideBannerLayout;
begin
    
    if (MODE = SILENTMODE) then
	    if (g_bShowIconsInListView) then		
			FeatureSelectItem ( MEDIA, TVSERIES_LISTICONS_FEATURE, TRUE );	
		else
			FeatureSelectItem ( MEDIA, TVSERIES_LISTICONS_FEATURE, FALSE );		
		endif;
    	return RES_PBUT_NEXT;
    endif;
    
	szDialogName = "SelectionTVSeries";
   	bDone = FALSE;
      	                         	                     
    EzDefineDialog(szDialogName,"","",RES_DLG_TVSERIESSELECT);

Dlg_Wait:  
    // Loop unitl done
    repeat
    	nCmdValue = WaitOnDialog(szDialogName);

	    switch (nCmdValue)
	    	
	    	case DLG_CLOSE:
	            // The user clicked the window's close button.
	            Do (EXIT);
	            
	        case DLG_ERR:
	            MessageBox ("Unable to display TVSeries selection dialog. Setup canceled.", SEVERE);
	            abort;
	            
	        case DLG_INIT:	        	     			     			     			
 				// Read from Global Variables
 				if g_bTVSeriesDefault then
 					CtrlSetState(szDialogName,RES_RADIO_CHOICE1,BUTTON_CHECKED);
 				else
 					CtrlSetState(szDialogName,RES_RADIO_CHOICE2,BUTTON_CHECKED);
 				endif;
 			
 				// Populate WideBanner layout list                               
	        	listWideBannerLayout = ListCreate(STRINGLIST);
	        	
	        	ListAddString(listWideBannerLayout,"Default",AFTER);	        	
	        	ListAddString(listWideBannerLayout,"5x2 Grid + Poster",AFTER);
	        	ListAddString(listWideBannerLayout,"5x3 Grid",AFTER);
	        	ListAddString(listWideBannerLayout,"7x3 Grid",AFTER);
	        	ListAddString(listWideBannerLayout,"10x4 Grid",AFTER);
	        		        	
	        	// Add Layouts to list
	        	CtrlSetList(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,listWideBannerLayout);	        	
	        	switch (g_sWideBannerLayout)
	        		case "0":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,"Default");
	        		case "1":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,"5x2 Grid + Poster");
	        		case "2":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,"5x3 Grid");
	        		case "3":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,"7x3 Grid");
	        		case "4":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,"10x4 Grid");
	        		default:
	        			CtrlSetCurSel(szDialogName,RES_COMBO_WIDEBANNERLAYOUT,"Default");
	        	endswitch;	        	
	        	ListDestroy(listWideBannerLayout);	        	
 			
 			case RES_BUTTON_TVSERIESADVANCED:
 				ShowTVSeriesAdvanced(bModify);
 			
 			case RES_HYPERLINK:
				if CtrlGetUrlForLinkClicked(szDialogName, RES_HYPERLINK, szUrl) = 0 then
					LaunchBrowser(szUrl);
				endif;
				    		      
			case RES_PBUT_BACK:
				bDone = TRUE;	 	        
	   	        	   	        	   	        
	        case RES_PBUT_NEXT:
	            bDone = TRUE;	        
	        
	        case RES_PBUT_CANCEL:
	        	Do (EXIT);
	        	
	        case RES_PBUT_CLOSE:
	        	Do (EXIT);
	        	
	     endswitch;
    
    until bDone;       
		
	bDone = FALSE;
	
	// Store settings in Global Variables
	if (CtrlGetState(szDialogName,RES_RADIO_CHOICE1) = BUTTON_CHECKED) then
		g_bTVSeriesDefault = TRUE;
	else
		g_bTVSeriesDefault = FALSE;
	endif;
	
	CtrlGetCurSel(szDialogName, RES_COMBO_WIDEBANNERLAYOUT, g_sWideBannerLayout); 
	switch (g_sWideBannerLayout)
		case "Default":
			g_sWideBannerLayout = "0";
		case "5x2 Grid + Poster":
			g_sWideBannerLayout = "1";
		case "5x3 Grid":
			g_sWideBannerLayout = "2";
		case "7x3 Grid":
			g_sWideBannerLayout = "3";
		case "10x4 Grid":
			g_sWideBannerLayout = "4";
	endswitch;
			
	bValidate = TRUE;	
	
	if !bValidate then 
	 	MessageBox(szErrorMsg,INFORMATION);	 	
		goto Dlg_Wait;
	endif;	
		
	if (g_bShowIconsInListView) then		
		FeatureSelectItem ( MEDIA, TVSERIES_LISTICONS_FEATURE, TRUE );	
	else
		FeatureSelectItem ( MEDIA, TVSERIES_LISTICONS_FEATURE, FALSE );		
	endif;

    EndDialog (szDialogName);
    ReleaseDialog (szDialogName);
        
   	return nCmdValue;
end;

function number ShowTVSeriesAdvanced(bModify)
	STRING szDialogName, szErrorMsg, svText;
	NUMBER nCmdValue;
	BOOL bDone, bValidate;	
begin

	szDialogName = "SelectionTVSeriesAdvanced";
   	bDone = FALSE;
      	                         	                     
    EzDefineDialog(szDialogName,"","",RES_DLG_TVSERIESADVANCED);

Dlg_Wait:  
    // Loop unitl done
    repeat
    	nCmdValue = WaitOnDialog(szDialogName);

	    switch (nCmdValue)
	    	
	    	case DLG_CLOSE:
	            // The user clicked the window's close button.
	            Do (EXIT);
	            
	        case DLG_ERR:
	            MessageBox ("Unable to display TVSeries Advanced dialog. Setup canceled.", SEVERE);
	            abort;
	            
	        case DLG_INIT:	        	     			     			     			
 				// Read from Global Variables
 				if g_bShowTotalEpisodeCount then
 					CtrlSetState(szDialogName,RES_CHECK_TOTALEPCOUNT,BUTTON_CHECKED); 			
 				endif;
 				if g_bShowIconsInListView then
 					CtrlSetState(szDialogName,RES_CHECK_LISTIMAGES,BUTTON_CHECKED); 			
 				endif; 				
 				 				
			case RES_PBUT_BACK:
				bDone = TRUE;	 	        
	   	        	   	        	   	        
	        case RES_PBUT_NEXT:
	            bDone = TRUE;	        
	        
	        case RES_PBUT_CANCEL:
	        	bDone = TRUE;
	        	
	        case RES_PBUT_CLOSE:
	        	bDone = TRUE;	    
	        	
	     endswitch;
    
    until bDone;       
		
	bDone = FALSE;
	
	// Store settings in Global Variables	
	if (nCmdValue = RES_PBUT_NEXT) then
		
		if CtrlGetState(szDialogName,RES_CHECK_TOTALEPCOUNT) = BUTTON_CHECKED then
			g_bShowTotalEpisodeCount = TRUE;
		else
			g_bShowTotalEpisodeCount = FALSE;
		endif;       
		if CtrlGetState(szDialogName,RES_CHECK_LISTIMAGES) = BUTTON_CHECKED then
			g_bShowIconsInListView = TRUE;
		else
			g_bShowIconsInListView = FALSE;
		endif;						
		
	endif;
	
    EndDialog (szDialogName);
    ReleaseDialog (szDialogName);
        
   	return nCmdValue;
end;

function number ShowSelectMovingPics(bModify)
	STRING szDialogName, szUrl;
	NUMBER nCmdValue;  
	LIST listThumbLayout;
	BOOL bDone;
begin
    
    if (MODE = SILENTMODE) then
    	return RES_PBUT_NEXT;
    endif;
    
	szDialogName = "SelectionMovingPictures";
   	bDone = FALSE;
      	                         	                     
    EzDefineDialog(szDialogName,"","",RES_DLG_MOVINGPICSSELECT);

Dlg_Wait:  
    // Loop unitl done
    repeat
    	nCmdValue = WaitOnDialog(szDialogName);

	    switch (nCmdValue)
	    	
	    	case DLG_CLOSE:
	            // The user clicked the window's close button.
	            Do (EXIT);
	            
	        case DLG_ERR:
	            MessageBox ("Unable to display Moving Pictures selection dialog. Setup canceled.", SEVERE);
	            abort;
	            
	        case DLG_INIT:	        	     			     			     			
 				// Read from Global Variables
 				if g_bMovingPicturesDefault then
 					CtrlSetState(szDialogName,RES_RADIO_CHOICE1,BUTTON_CHECKED);
 				else
 					CtrlSetState(szDialogName,RES_RADIO_CHOICE2,BUTTON_CHECKED);
 				endif;
 			              
 			    // Populate Thumbs layout list                               
	        	listThumbLayout = ListCreate(STRINGLIST);
	        	
	        	ListAddString(listThumbLayout,"Default",AFTER);
	        	ListAddString(listThumbLayout,"[12x4][8x3] Grid",AFTER);
	        	ListAddString(listThumbLayout,"[12x3][8x2] Grid + Info",AFTER);
	        		        	
	        	// Add Layouts to list
	        	CtrlSetList(szDialogName, RES_COMBO_THUMBLAYOUT, listThumbLayout);	        	
	        	switch (g_sThumbLayout)
	        		case "0":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_THUMBLAYOUT,"Default");
	        		case "1":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_THUMBLAYOUT,"[12x4][8x3] Grid");
	        		case "2":
	        			CtrlSetCurSel(szDialogName,RES_COMBO_THUMBLAYOUT,"[12x3][8x2] Grid + Info");
	        		default:
	        			CtrlSetCurSel(szDialogName,RES_COMBO_THUMBLAYOUT,"Default");	        		
	        	endswitch;	        	
	        	ListDestroy(listThumbLayout);	  
 			              
 			case RES_HYPERLINK:
				if CtrlGetUrlForLinkClicked(szDialogName, RES_HYPERLINK, szUrl) = 0 then
					LaunchBrowser(szUrl);
				endif;
				
			case RES_PBUT_BACK:
				bDone = TRUE;	 	        
	   	        	   	        	   	        
	        case RES_PBUT_NEXT:
	            bDone = TRUE;	        
	        
	        case RES_PBUT_CANCEL:
	        	Do (EXIT);
	        	
	        case RES_PBUT_CLOSE:
	        	Do (EXIT);
	        	
	     endswitch;
    
    until bDone;       
		
	bDone = FALSE;
	
	// Store settings in Global Variables
	if (CtrlGetState(szDialogName,RES_RADIO_CHOICE1) = BUTTON_CHECKED) then
		g_bMovingPicturesDefault = TRUE;
	else
		g_bMovingPicturesDefault = FALSE;
	endif;
	
	CtrlGetCurSel(szDialogName, RES_COMBO_THUMBLAYOUT, g_sThumbLayout); 
	switch (g_sThumbLayout)
		case "Default":
			g_sThumbLayout = "0";
		case "[12x4][8x3] Grid":
			g_sThumbLayout = "1";
		case "[12x3][8x2] Grid + Info":
			g_sThumbLayout = "2";	
	endswitch;
		
    EndDialog (szDialogName);
    ReleaseDialog (szDialogName);
        
   	return nCmdValue;
end;

function number ShowSelectOptions(bModify)
	STRING szDialogName, sValue;
	NUMBER nCmdValue;
	BOOL bDone, bResult;
	LIST listFonts, listTVGuides, listVUMeters, listTVMiniGuides;
	OBJECT objColors, objUnfocusedAlpha;
begin
    
    if (MODE = SILENTMODE) then
    	if (!g_bLargeFonts) then
			FeatureSelectItem ( MEDIA, FONTSDEF_FEATURE_OPTIONS, TRUE );
			FeatureSelectItem ( MEDIA, FONTSLGE_FEATURE_OPTIONS, FALSE );
		else
			FeatureSelectItem ( MEDIA, FONTSDEF_FEATURE_OPTIONS, FALSE );
			FeatureSelectItem ( MEDIA, FONTSLGE_FEATURE_OPTIONS, TRUE );
		endif;
    	return RES_PBUT_NEXT;
    endif;
    
	szDialogName = "SelectionOptions";
   	bDone = FALSE;
      	                         	                     
    EzDefineDialog(szDialogName,"","",RES_DLG_OPTIONS);

Dlg_Wait:  
    // Loop unitl done
    repeat
    	nCmdValue = WaitOnDialog(szDialogName);

	    switch (nCmdValue)
	    	
	    	case DLG_CLOSE:
	            // The user clicked the window's close button.
	            Do (EXIT);
	            
	        case DLG_ERR:
	            MessageBox ("Unable to display Options dialog. Setup canceled.", SEVERE);
	            abort;
	            
	        case DLG_INIT:
	        	// Populate font size list                               
	        	listFonts = ListCreate(STRINGLIST);
	        	
	        	ListAddString(listFonts,"Default",AFTER);
	        	ListAddString(listFonts,"Large",AFTER);
	        
	        	// Add Font sizes to list
	        	CtrlSetList(szDialogName,RES_COMBO_FONTS,listFonts);
	      		
	      		// Select Last/Default Font size
	      		if g_bLargeFonts then
	   				CtrlSetCurSel(szDialogName,RES_COMBO_FONTS,"Large");		   		                                      
	   			else
	   				CtrlSetCurSel(szDialogName,RES_COMBO_FONTS,"Default");
	   			endif;
				
 				ListDestroy(listFonts);
	      		                                      
	      		// Populate TVGuide size list
	      		listTVGuides = ListCreate(STRINGLIST);	      			    	
	        	        	     			     			     			 				
 				ListAddString(listTVGuides,"8 Rows",AFTER);
	        	ListAddString(listTVGuides,"10 Rows",AFTER);
	        	ListAddString(listTVGuides,"12 Rows",AFTER);	    	    		
	        	ListAddString(listTVGuides,"16 Rows",AFTER);
	        	
	        	// Add TVGuide sizes to list
	        	CtrlSetList(szDialogName,RES_COMBO_TVGUIDE,listTVGuides);
	    
 				// Select Last/Default TVGuide size
 				switch (g_sTVGuideSize)
 					case "8":
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVGUIDE,"8 Rows");
	    			case "10":
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVGUIDE,"10 Rows"); 
	    			case "16":
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVGUIDE,"16 Rows");
	    			default:
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVGUIDE,"12 Rows");
	   			endswitch; 		
				ListDestroy(listTVGuides);					    			 				
 				
 				// Populate TVMiniGuide size list
	      		listTVMiniGuides = ListCreate(STRINGLIST);	      			    	
	        	        	     			     			     			 				
 				ListAddString(listTVMiniGuides,"3 Rows",AFTER);
	        	ListAddString(listTVMiniGuides,"5 Rows",AFTER);
	        	ListAddString(listTVMiniGuides,"7 Rows",AFTER);	    	    		
	        	ListAddString(listTVMiniGuides,"9 Rows",AFTER);
	        	
	        	// Add TVGuide sizes to list
	        	CtrlSetList(szDialogName,RES_COMBO_TVMINIGUIDE,listTVMiniGuides);
	    
 				// Select Last/Default TVGuide size
 				switch (g_sTVMiniGuideSize)
 					case "3":
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVMINIGUIDE,"3 Rows");
	    			case "5":
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVMINIGUIDE,"5 Rows");
	    			case "9":
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVMINIGUIDE,"9 Rows");
	    			default:
	    				CtrlSetCurSel(szDialogName,RES_COMBO_TVMINIGUIDE,"7 Rows");
	   			endswitch; 		
				ListDestroy(listTVMiniGuides);
 				 			 				 				 		
 				if g_bSetAsDefaultSkin then
 					CtrlSetState(szDialogName,RES_CHECK_SKINDEFAULT,BUTTON_CHECKED);
 				endif;
 				
 				if g_bStartBasicHome then
 					CtrlSetState(szDialogName,RES_CHECK_USEBASICHOME,BUTTON_CHECKED);
 				endif;  
 				
 				if g_bShowRoundedCovers then
 					CtrlSetState(szDialogName,RES_CHECK_ROUNDEDCOVERS,BUTTON_CHECKED);
 				endif;
 				       
 				if g_bShowHiddenMenuImage then
 					CtrlSetState(szDialogName,RES_CHECK_SHOWHIDDENMENUIMAGE,BUTTON_CHECKED);
 				endif;
 				  
 				if g_bEnableRandomFanartInMyTV then
 					CtrlSetState(szDialogName,RES_CHECK_ENABLERANDOMFANARTMYTV,BUTTON_CHECKED);
 				endif; 
 				
 				if g_bShowArtworkIcons then
 					CtrlSetState(szDialogName,RES_CHECK_ARTWORKICONS,BUTTON_CHECKED); 			
 				endif; 			 				
 				 				
 			case RES_BUTTON_LISTCOLOURS:
 				// Create Instance of Colors Class
 				try
	 				set objColors = CoCreateObjectDotNet(SUPPORTDIR^"FacadeProperties.dll", "FacadeProperties.Colors" );
					
					// Update Defaults
					objColors.SetTextColor(g_sTextColor);
					objColors.SetText2Color(g_sText2Color);
					objColors.SetText3Color(g_sText3Color);
					objColors.SetWatchedColor(g_sWatchedColor);
					objColors.SetRemoteColor(g_sRemoteColor);
									
					// Display Color Choice Dialog
					bResult = objColors.ShowColors(); 
		            
		            // Save Colors
					if bResult then					
						g_sTextColor = objColors.GetTextColor();
						g_sText2Color = objColors.GetText2Color();
						g_sText3Color = objColors.GetText3Color();
						g_sWatchedColor = objColors.GetWatchedColor();
						g_sRemoteColor = objColors.GetRemoteColor();					
					endif;
	 			    
	 			    // Destroy Object
	 			    set objColors = NOTHING;
	 			 catch
	 			 	MessageBox("Error: unable to display\read colours",INFORMATION);
	 			 endcatch;
 			
 			case RES_BUTTON_UNFOCUSEDALPHA:
 				 // Create Instance of UnfocusedAlpha Class
 				try
	 				set objUnfocusedAlpha = CoCreateObjectDotNet(SUPPORTDIR^"FacadeProperties.dll", "FacadeProperties.UnfocusedAlpha" );
					
					// Update Defaults
					objUnfocusedAlpha.SetUnfocusedAlphaListItems(g_sUnfocusedAlphaListItems);
					objUnfocusedAlpha.SetUnfocusedAlphaThumbs(g_sUnfocusedAlphaThumbs);
														
					// Display Color Choice Dialog
					bResult = objUnfocusedAlpha.ShowUnfocusedAlpha(); 
		            
		            // Save Colors
					if bResult then					
                    	g_sUnfocusedAlphaListItems =  objUnfocusedAlpha.GetUnfocusedAlphaListItems;
                    	g_sUnfocusedAlphaThumbs = objUnfocusedAlpha.GetUnfocusedAlphaThumbs;
					endif;
	 			    
	 			    // Destroy Object
	 			    set objColors = NOTHING;
	 			 catch
	 			 	MessageBox("Error: unable to display\read colours",INFORMATION);
	 			 endcatch;
 			     				 				         		      
			case RES_PBUT_BACK:
				bDone = TRUE;	 	        
	   	        	   	        	   	        
	        case RES_PBUT_NEXT:
	            bDone = TRUE;	        
	        
	        case RES_PBUT_CANCEL:
	        	Do (EXIT);
	        	
	        case RES_PBUT_CLOSE:
	        	Do (EXIT);
	        	
	     endswitch;
    
    until bDone;       
		
	bDone = FALSE;
	
	// Store settings in Global Variables
	
	CtrlGetCurSel(szDialogName,RES_COMBO_TVGUIDE,sValue);
	switch (sValue)
		case "8 Rows":
			g_sTVGuideSize = "8"; 
		case "10 Rows":
			g_sTVGuideSize = "10";  
		case "16 Rows":
			g_sTVGuideSize = "16";
		default:
			g_sTVGuideSize = "12";
	endswitch;  
	
	CtrlGetCurSel(szDialogName,RES_COMBO_TVMINIGUIDE,sValue);
	switch (sValue)
		case "3 Rows":
			g_sTVMiniGuideSize = "3"; 
		case "5 Rows":
			g_sTVMiniGuideSize = "5";
		case "9 Rows":
			g_sTVMiniGuideSize = "9";
		default:
			g_sTVMiniGuideSize = "7";
	endswitch;

	CtrlGetCurSel(szDialogName,RES_COMBO_FONTS,sValue);	
	if sValue = "Large" then
		g_bLargeFonts = TRUE;
	else
		g_bLargeFonts = FALSE;
	endif;
	
	if (CtrlGetState(szDialogName,RES_CHECK_SKINDEFAULT) = BUTTON_CHECKED) then
		g_bSetAsDefaultSkin = TRUE;
	else
		g_bSetAsDefaultSkin = FALSE;
	endif;  
	
	if (CtrlGetState(szDialogName,RES_CHECK_USEBASICHOME) = BUTTON_CHECKED) then
		g_bStartBasicHome = TRUE;
	else
		g_bStartBasicHome = FALSE;
	endif;  	
	
	if (CtrlGetState(szDialogName,RES_CHECK_ROUNDEDCOVERS) = BUTTON_CHECKED) then
		g_bShowRoundedCovers = TRUE;
	else
		g_bShowRoundedCovers = FALSE;
	endif;  
	  
	if (CtrlGetState(szDialogName,RES_CHECK_ENABLERANDOMFANARTMYTV) = BUTTON_CHECKED) then
		g_bEnableRandomFanartInMyTV = TRUE;		
	else
		g_bEnableRandomFanartInMyTV = FALSE;		
	endif;  	
	
	if CtrlGetState(szDialogName,RES_CHECK_ARTWORKICONS) = BUTTON_CHECKED then
		g_bShowArtworkIcons = TRUE;
	else
		g_bShowArtworkIcons = FALSE;
	endif;	 
	
	if CtrlGetState(szDialogName,RES_CHECK_SHOWHIDDENMENUIMAGE) = BUTTON_CHECKED then
		g_bShowHiddenMenuImage = TRUE;
	else
		g_bShowHiddenMenuImage = FALSE;
	endif;
		
	// Set Features 
	
	// Font Sizes	
	if (!g_bLargeFonts) then
		FeatureSelectItem ( MEDIA, FONTSDEF_FEATURE_OPTIONS, TRUE );
		FeatureSelectItem ( MEDIA, FONTSLGE_FEATURE_OPTIONS, FALSE );
	else
		FeatureSelectItem ( MEDIA, FONTSDEF_FEATURE_OPTIONS, FALSE );
		FeatureSelectItem ( MEDIA, FONTSLGE_FEATURE_OPTIONS, TRUE );
	endif;	

    EndDialog (szDialogName);
    ReleaseDialog (szDialogName);
        
   	return nCmdValue;
end;


function number ShowSelectNowPlaying(bModify)
	STRING szDialogName, sValue;
	NUMBER nCmdValue;
	BOOL bDone, bResult;
	LIST listNowPlaying;	
begin
    
    if (MODE = SILENTMODE) then
	    FeatureSelectItem ( MEDIA, NOWPLAYING_DEFAULT, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_HIDDENMENU, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_SLIDINGMENU, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_FADE, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOFADE, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOMASK, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_FADE, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOFADE, FALSE );
	    FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOMASK, FALSE );
    	switch (g_sNowPlayingChoice)	
			case "0":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_DEFAULT, TRUE );	
			case "1":			
			    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_HIDDENMENU, TRUE );			    
			case "2":				
			    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_SLIDINGMENU, TRUE );			   
			case "3":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_FADE, TRUE );				
			case "4":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOFADE, TRUE );			
			case "5":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOMASK, TRUE );			
			case "6":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_FADE, TRUE );			
			case "7":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOFADE, TRUE );			
			case "8":				
				FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOMASK, TRUE );							
			default:                 
				g_sNowPlayingChoice = "0";
				FeatureSelectItem ( MEDIA, NOWPLAYING_DEFAULT, TRUE );			
		endswitch;
    	return RES_PBUT_NEXT;
    endif;
    
	szDialogName = "SelectionNowPlaying";
   	bDone = FALSE;
      	                         	                     
    EzDefineDialog(szDialogName,"","",RES_DLG_MUSICNOWPLAYING);

Dlg_Wait:  
    // Loop unitl done
    repeat
    	nCmdValue = WaitOnDialog(szDialogName);

	    switch (nCmdValue)
	    	
	    	case DLG_CLOSE:
	            // The user clicked the window's close button.
	            Do (EXIT);
	            
	        case DLG_ERR:
	            MessageBox ("Unable to display Now Playing dialog. Setup canceled.", SEVERE);
	            abort;
	            
	        case DLG_INIT:
	        	// Populate font size list                               
	        	listNowPlaying = ListCreate(STRINGLIST);
	        	
	        	ListAddString(listNowPlaying,"Default",AFTER);
	        	ListAddString(listNowPlaying,"Fullscreen - Standard Hidden Menu",AFTER);
	        	ListAddString(listNowPlaying,"Fullscreen - Top/Down Sliding Menu",AFTER);
	        	ListAddString(listNowPlaying,"Default - Edge Fade",AFTER);
	        	ListAddString(listNowPlaying,"Default - Edge No Fade",AFTER);
	        	ListAddString(listNowPlaying,"Default - Edge No Mask",AFTER);
	        	ListAddString(listNowPlaying,"Default - Window Fade",AFTER);
	        	ListAddString(listNowPlaying,"Default - Window No Fade",AFTER);
	        	ListAddString(listNowPlaying,"Default - Window No Mask",AFTER);
	        	
	        	// Add Now Playing styles to list
	        	CtrlSetList(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,listNowPlaying);
	      		
	      		// Set current selection
	      		switch (g_sNowPlayingChoice)	
					case "0": //"Default":
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default");
					
					case "1": //"Fullscreen - Standard Hidden Menu":                        
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Fullscreen - Standard Hidden Menu");					   
					    
					case "2": //"Fullscreen - Top/Down Sliding Menu":                                           
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Fullscreen - Top/Down Sliding Menu");					    
					    
					case "3": //"Default - Edge Fade":                                                                    
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default - Edge Fade");						
						
					case "4": //"Default - Edge No Fade":
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default - Edge No Fade");
					
					case "5": //"Default - Edge No Mask":
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default - Edge No Mask");						
					
					case "6": //"Default - Window Fade":
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default - Window Fade");
					
					case "7": //"Default - Window No Fade":
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default - Window No Fade");						
					
					case "8": // "Default - Window No Mask":
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default - Window No Mask");						
									
					default:
						CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,"Default");	
					
				endswitch;
				  
	    		CtrlSetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,g_sNowPlayingChoice); 				
 				ListDestroy(listNowPlaying);
	       		
	       		// Show corresponding Preview image
	       		ShowNowPlayingPreviewImage(szDialogName); 				 				 			
 			     				 				         		      
			case RES_COMBO_NOWPLAYINGCHOICE:
				ShowNowPlayingPreviewImage(szDialogName);
			  			     				 				         		      
			case RES_PBUT_BACK:
				bDone = TRUE;	 	        
	   	        	   	        	   	        
	        case RES_PBUT_NEXT:
	            bDone = TRUE;	        
	        
	        case RES_PBUT_CANCEL:
	        	Do (EXIT);
	        	
	        case RES_PBUT_CLOSE:
	        	Do (EXIT);
	        	
	     endswitch;
    
    until bDone;       
		
	bDone = FALSE;
	
	// Store settings in Global Variables	
	CtrlGetCurSel(szDialogName,RES_COMBO_NOWPLAYINGCHOICE,sValue);
    
    // Set Feature Selection
    FeatureSelectItem ( MEDIA, NOWPLAYING_DEFAULT, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_HIDDENMENU, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_SLIDINGMENU, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_FADE, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOFADE, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOMASK, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_FADE, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOFADE, FALSE );
    FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOMASK, FALSE );
        
    switch (sValue)	
		case "Default":
			g_sNowPlayingChoice = "0";
			FeatureSelectItem ( MEDIA, NOWPLAYING_DEFAULT, TRUE );
		
		case "Fullscreen - Standard Hidden Menu":
			g_sNowPlayingChoice = "1";
		    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_HIDDENMENU, TRUE );
		    
		case "Fullscreen - Top/Down Sliding Menu":
			g_sNowPlayingChoice = "2";
		    FeatureSelectItem ( MEDIA, NOWPLAYING_FULLSCREEN_SLIDINGMENU, TRUE );
		    
		case "Default - Edge Fade":
			g_sNowPlayingChoice = "3";
			FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_FADE, TRUE );  
			
		case "Default - Edge No Fade":
			g_sNowPlayingChoice = "4";
			FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOFADE, TRUE );
		
		case "Default - Edge No Mask":
			g_sNowPlayingChoice = "5";
			FeatureSelectItem ( MEDIA, NOWPLAYING_EDGE_NOMASK, TRUE );
		
		case "Default - Window Fade":
			g_sNowPlayingChoice = "6";
			FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_FADE, TRUE );
		
		case "Default - Window No Fade":
			g_sNowPlayingChoice = "7";
			FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOFADE, TRUE );
		
		case "Default - Window No Mask":
			g_sNowPlayingChoice = "8";
			FeatureSelectItem ( MEDIA, NOWPLAYING_WINDOW_NOMASK, TRUE );
						
		default:                 
			g_sNowPlayingChoice = "0";
			FeatureSelectItem ( MEDIA, NOWPLAYING_DEFAULT, TRUE );	
		
	endswitch;
    
    EndDialog (szDialogName);
    ReleaseDialog (szDialogName);
        
   	return nCmdValue;
end;

function void ShowNowPlayingPreviewImage(szDlg)
	STRING svSelection;
begin
			
	// Hide all controls
	HideControl(szDlg, RES_BMP_NOWPLAYINGDEFAULT);
	HideControl(szDlg, RES_BMP_NOWPLAYINGFULLSCREENHIDDENMENU);
	HideControl(szDlg, RES_BMP_NOWPLAYINGFULLSCREENSLIDINGMENU);
	HideControl(szDlg, RES_BMP_NOWPLAYINGEDGEFADE);
	HideControl(szDlg, RES_BMP_NOWPLAYINGEDGENOFADE);
	HideControl(szDlg, RES_BMP_NOWPLAYINGEDGENOMASK);
	HideControl(szDlg, RES_BMP_NOWPLAYINGWINDOWFADE);
	HideControl(szDlg, RES_BMP_NOWPLAYINGWINDOWNOFADE);
	HideControl(szDlg, RES_BMP_NOWPLAYINGWINDOWNOMASK);
		
	// Show Selected Preview image
	CtrlGetCurSel(szDlg, RES_COMBO_NOWPLAYINGCHOICE, svSelection);
	
	switch (svSelection)	
		case "Default":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGDEFAULT);
		
		case "Fullscreen - Standard Hidden Menu":
		    ShowControl(szDlg, RES_BMP_NOWPLAYINGFULLSCREENHIDDENMENU);
		    
		case "Fullscreen - Top/Down Sliding Menu":
		    ShowControl(szDlg, RES_BMP_NOWPLAYINGFULLSCREENSLIDINGMENU);
		    
		case "Default - Edge Fade":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGEDGEFADE);  
			
		case "Default - Edge No Fade":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGEDGENOFADE);
		
		case "Default - Edge No Mask":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGEDGENOMASK);
		
		case "Default - Window Fade":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGWINDOWFADE);
		
		case "Default - Window No Fade":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGWINDOWNOFADE);
		
		case "Default - Window No Mask":
			ShowControl(szDlg, RES_BMP_NOWPLAYINGWINDOWNOMASK);
						
		default:
			ShowControl(szDlg, RES_BMP_NOWPLAYINGDEFAULT);	
		
	endswitch;	
	
end;

function void HideControl(szDlg, controlID)
	HWND  	hwndDlg, hCtrl;
	NUMBER	nResult;
begin
      
    // Get Handle of Dialog
	hwndDlg = CmdGetHwndDlg( szDlg );
	
	// Get Handle of Control
	hCtrl = GetDlgItem(hwndDlg, controlID); 
	
	// Hide Control
	nResult = ShowWindow(hCtrl, SW_HIDE);
	
end;

function void ShowControl(szDlg, controlID)
	HWND  	hwndDlg, hCtrl;
	NUMBER	nResult;
begin
      
    // Get Handle of Dialog
	hwndDlg = CmdGetHwndDlg( szDlg );
	
	// Get Handle of Control
	hCtrl = GetDlgItem(hwndDlg, controlID); 
	
	// Hide Control
	nResult = ShowWindow(hCtrl, SW_SHOW);
	
end;

function NUMBER CheckStringExists(sFile, sSearchStr)
	LIST listID;
	NUMBER nResult;
	STRING svString;
	BOOL bMatch;
begin
	// check if file exists
	if !(Is(FILE_EXISTS, sFile)) then
		return -1;
	endif;
	
	// check if the string exists
	listID = ListCreate(STRINGLIST);
	ListReadFromFile ( listID, sFile );
	
	nResult = ListGetFirstString(listID, svString);
	
	while (nResult != END_OF_LIST)		
		if (svString % sSearchStr) then		
			// Match Found
			bMatch = TRUE;
			nResult = ListSetCurrentItem(listID, LISTLAST);
		endif;
		nResult = ListGetNextString(listID, svString);
	endwhile;
	
	ListDestroy(listID); 
	
	if (bMatch) then
		return 1;
	else
		return 0;
	endif;
	
end;

function VOID CheckXMLSectionsExist()
	STRING TVSeriesSection,  MovingPicturesSection, MusicSection, TVSection, MiscSection, InstallSection;
	STRING SearchStr, sFile;
begin
    
    sFile = g_sConfigDir^"StreamedMPConfig.xml";
    
    // TVseries Section
    TVSeriesSection = 		"\t<section name=\"TVSeriesConfig\">"+
    						"\n\t\t<entry name=\"tvseriesDefaultStyle\">1</entry>"+
    						"\n\t\t<entry name=\"tvseriesWideBannerMod\">0</entry>"+
    						"\n\t\t<entry name=\"tvseriesShowTotalEpisodeCounts\">0</entry>"+
    						"\n\t\t<entry name=\"tvseriesShowIconsInListView\">1</entry>"+    						
    						"\n\t</section>"+
    						"\n</profile>";
    
    SearchStr = "<section name=\"TVSeriesConfig\">";
    if CheckStringExists(sFile, SearchStr) = 0 then
    	ReplaceLineInFile(sFile, "</profile>", TVSeriesSection);
    endif;
	
	// MovingPictures Section    						
	MovingPicturesSection = "\t<section name=\"MovingPicturesConfig\">"+
							"\n\t\t<entry name=\"movingpicturesDefaultStyle\">1</entry>"+
							"\n\t\t<entry name=\"movingpicturesThumbMod\">0</entry>"+
							"\n\t</section>"+
							"\n</profile>";
							
	SearchStr = "<section name=\"MovingPicturesConfig\">";
    if CheckStringExists(sFile, SearchStr) = 0 then
    	ReplaceLineInFile(sFile, "</profile>", MovingPicturesSection);
    endif; 
    
    // Music Section
    MusicSection = 			"\t<section name=\"MusicConfig\">"+
							"\n\t\t<entry name=\"musicNowPlayingStyle\">0</entry>"+							
							"\n\t</section>"+
							"\n</profile>";
							
	SearchStr = "<section name=\"MusicConfig\">";
    if CheckStringExists(sFile, SearchStr) = 0 then
    	ReplaceLineInFile(sFile, "</profile>", MusicSection);
    endif;
    
    // TV Section
    TVSection = 			"\t<section name=\"TVConfig\">"+
							"\n\t\t<entry name=\"tvGuideSize\">10</entry>"+							
							"\n\t\t<entry name=\"tvMiniGuideSize\">7</entry>"+
							"\n\t\t<entry name=\"tvEnableRandomTVSeriesFanart\">0</entry>"+
							"\n\t</section>"+
							"\n</profile>";
							
	SearchStr = "<section name=\"TVConfig\">";
    if CheckStringExists(sFile, SearchStr) = 0 then
    	ReplaceLineInFile(sFile, "</profile>", TVSection);
    endif;
    
    // Misc Section
    MiscSection = 			"\t<section name=\"MiscConfig\">"+
							"\n\t\t<entry name=\"miscUseLargeFonts\">0</entry>"+							
							"\n\t\t<entry name=\"miscShowHiddenMenuImage\">1</entry>"+
							"\n\t\t<entry name=\"miscShowIconsInArtwork\">1</entry>"+
							"\n\t\t<entry name=\"miscShowRoundedCovers\">1</entry>"+
							"\n\t\t<entry name=\"miscUnfocusedAlphaListItems\">100</entry>"+
							"\n\t\t<entry name=\"miscUnfocusedAlphaThumbs\">100</entry>"+
							"\n\t\t<entry name=\"miscTextColor\">FFFFFF</entry>"+
							"\n\t\t<entry name=\"miscTextColor2\">FFFFFF</entry>"+
							"\n\t\t<entry name=\"miscTextColor3\">FFFFFF</entry>"+							
							"\n\t\t<entry name=\"miscWatchedColor\">808080</entry>"+
							"\n\t\t<entry name=\"miscRemoteColor\">FFA075</entry>"+
							"\n\t</section>"+
							"\n</profile>";
							
	SearchStr = "<section name=\"MiscConfig\">";
    if CheckStringExists(sFile, SearchStr) = 0 then
    	ReplaceLineInFile(sFile, "</profile>", MiscSection);
    endif; 
    
    // Install Features
    InstallSection = 		"\t<section name=\"InstallFeatures\">"+
							"\n\t\t<entry name=\"tvLogosAustralia\">0</entry>"+							
							"\n\t\t<entry name=\"tvLogosGermanyClassical\">0</entry>"+
							"\n\t\t<entry name=\"tvLogosGermanyModern\">0</entry>"+
							"\n\t\t<entry name=\"tvLogosGermanyTransparent\">0</entry>"+
							"\n\t\t<entry name=\"tvLogosCanada\">0</entry>"+
							"\n\t\t<entry name=\"tvLogosUK\">0</entry>"+							
							"\n\t</section>"+
							"\n</profile>";
							
	SearchStr = "<section name=\"InstallFeatures\">";
    if CheckStringExists(sFile, SearchStr) = 0 then
    	ReplaceLineInFile(sFile, "</profile>", InstallSection);
    endif; 
    
end;

function VOID ApplyCheckSum()
	OBJECT objCheckSum;
begin
	if (MODE != SILENTMODE) then
		SdShowMsg("Applying Checksums...Please wait.", TRUE);
	endif;

	// Apply Check sum to all Installed XML files.
	try		             	
		set objCheckSum = CoCreateObjectDotNet(g_sMediaPortalProgramDir^"SMPCheckSum.dll", "SMPCheckSum.CheckSum" );
		objCheckSum.ReplaceAllFiles(INSTALLDIR^SKIN_NAME);
	catch
		if (MODE!=SILENTMODE) then
	    	MessageBox("Error occurred applying CheckSums to XML files",INFORMATION);
	    endif;
	endcatch;
	
	// Release CheckSum Object		
	set objCheckSum = NOTHING;                           
	
	if (MODE != SILENTMODE) then
		SdShowMsg("Applying Checksums...Please wait.", FALSE);
	endif;
	
end;

function VOID UpdateSettings()
	STRING svString, sFile, sArtistImage, szDefine, sStyle, svValue;
	BOOL bReturn;
		
begin
	// Confirm if configuration sections exist
	// we dont have any XML functions to help here	
   	CheckXMLSectionsExist();  	

	// Save configuration	
	sFile = g_sConfigDir^"StreamedMPConfig.xml";
	SetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesDefaultStyle", BoolToStringInt(g_bTVSeriesDefault));
	SetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesWideBannerMod", g_sWideBannerLayout);	
	SetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesShowTotalEpisodeCounts", BoolToStringInt(g_bShowTotalEpisodeCount));
	SetMPXMLProperty(sFile, "TVSeriesConfig", "tvseriesShowIconsInListView", BoolToStringInt(g_bShowIconsInListView));	
	
	SetMPXMLProperty(sFile, "MovingPicturesConfig", "movingpicturesDefaultStyle", BoolToStringInt(g_bMovingPicturesDefault));
	SetMPXMLProperty(sFile, "MovingPicturesConfig", "movingpicturesThumbMod", g_sThumbLayout);
    
    SetMPXMLProperty(sFile, "TVConfig", "tvGuideSize", g_sTVGuideSize);
    SetMPXMLProperty(sFile, "TVConfig", "tvMiniGuideSize", g_sTVMiniGuideSize);
    SetMPXMLProperty(sFile, "TVConfig", "tvEnableRandomTVSeriesFanart", BoolToStringInt(g_bEnableRandomFanartInMyTV));
    
    SetMPXMLProperty(sFile, "MusicConfig", "musicNowPlayingStyle", g_sNowPlayingChoice);
    
	SetMPXMLProperty(sFile, "MiscConfig", "miscUseLargeFonts", BoolToStringInt(g_bLargeFonts));
	SetMPXMLProperty(sFile, "MiscConfig", "miscShowHiddenMenuImage", BoolToStringInt(g_bShowHiddenMenuImage));
	SetMPXMLProperty(sFile, "MiscConfig", "miscShowIconsInArtwork", BoolToStringInt(g_bShowArtworkIcons));
	SetMPXMLProperty(sFile, "MiscConfig", "miscShowRoundedCovers", BoolToStringInt(g_bShowRoundedCovers));
	SetMPXMLProperty(sFile, "MiscConfig", "miscUnfocusedAlphaListItems", g_sUnfocusedAlphaListItems);
	SetMPXMLProperty(sFile, "MiscConfig", "miscUnfocusedAlphaThumbs", g_sUnfocusedAlphaThumbs);
	SetMPXMLProperty(sFile, "MiscConfig", "miscTextColor", g_sTextColor);
	SetMPXMLProperty(sFile, "MiscConfig", "miscTextColor2", g_sText2Color);
	SetMPXMLProperty(sFile, "MiscConfig", "miscTextColor3", g_sText3Color);
	SetMPXMLProperty(sFile, "MiscConfig", "miscWatchedColor", g_sWatchedColor);
	SetMPXMLProperty(sFile, "MiscConfig", "miscRemoteColor", g_sRemoteColor);
	
	SetMPXMLProperty(sFile, "InstallFeatures", "tvLogosAustralia", g_sTVLogosAustralia);
	SetMPXMLProperty(sFile, "InstallFeatures", "tvLogosGermanyClassical", g_sTVLogosGermanyClassical);
	SetMPXMLProperty(sFile, "InstallFeatures", "tvLogosGermanyModern", g_sTVLogosGermanyModern);
	SetMPXMLProperty(sFile, "InstallFeatures", "tvLogosGermanyTransparent", g_sTVLogosGermanyTransparent);
	SetMPXMLProperty(sFile, "InstallFeatures", "tvLogosCanada", g_sTVLogosCanada);
	SetMPXMLProperty(sFile, "InstallFeatures", "tvLogosUK", g_sTVLogosUK);
	
	// Apply Configuration
		                                      
	// Update Graphics Quality for SeriesPosters, Default style does not require as much details
	// as it uses a much smaller Filmstrip and also contains a 100% Image on item selected       
	sFile = STREAMEDMP_SKIN_TVSERIESSETTINGS;
	if g_bTVSeriesDefault then
		SetXMLProperty(sFile, "/settings/graphicsquality/seriesposters", "20");
	else
		SetXMLProperty(sFile, "/settings/graphicsquality/seriesposters", "35");
	endif;
	
	// Set Total Episode Count Field to Series/Season List View
	sFile = STREAMEDMP_SKIN_TVSERIESSETTINGS;
	if g_bShowTotalEpisodeCount then	
		SetXMLProperty(sFile, "/settings/views/series/item3", "SeriesTotalEpisodes");
		SetXMLProperty(sFile, "/settings/views/season/item3", "SeasonTotalEpisodes");	
	else
		// Display nothing for Season List
		SetXMLProperty(sFile, "/settings/views/series/item3", "SMALLSPACE");		
		SetXMLProperty(sFile, "/settings/views/season/item3", "SMALLSPACE");
	endif;
	
	// Set Watched/Unwatched counts to Series/Season List View
	if g_bShowIconsInListView then
		SetXMLProperty(sFile, "/settings/views/series/item1", "SeriesWatchedAndUnWatched");
		SetXMLProperty(sFile, "/settings/views/season/item1", "SeasonWatchedAndUnWatched");		 
	else				
		// Display the Series Air Day and nothing for Season List
		SetXMLProperty(sFile, "/settings/views/series/item1", "SeriesAirsDay");		
		SetXMLProperty(sFile, "/settings/views/season/item1", "SMALLSPACE");
	endif;				 
		
	// Adjust Skin to support 1920x1080 resolution
	bReturn = AdjustForScreenSize();
	ModifySkinFilesForFULLHD(bReturn);

	// Update TVSeries Imports
	sFile = STREAMEDMP_SKIN_TVSERIES;
	
	sStyle = "Default";
	if !g_bTVSeriesDefault then
		sStyle = "Fanart";
	endif;
	
	SetXMLProperty(sFile, TVSERIES_SERIESANDSEASONLISTPOSTERS_PATH, "TVSeries."+sStyle+".SeriesAndSeasonListPosters.xml");
	SetXMLProperty(sFile, TVSERIES_SERIESANDSEASONFILMSTRIP_PATH, "TVSeries."+sStyle+".SeriesAndSeasonFilmstrip.xml");
	SetXMLProperty(sFile, TVSERIES_SERIESLISTBANNERS_PATH, "TVSeries."+sStyle+".SeriesListBanners.xml");
	SetXMLProperty(sFile, TVSERIES_EPISODELIST_PATH, "TVSeries."+sStyle+".EpisodeList.xml");
	SetXMLProperty(sFile, TVSERIES_GROUPLIST_PATH, "TVSeries."+sStyle+".GroupList.xml");
	
	switch (g_sWideBannerLayout)
		case "0":
			SetXMLProperty(sFile, TVSERIES_FACADE_PATH, "TVSeries."+sStyle+".Facade.xml");
			SetXMLProperty(sFile, TVSERIES_SERIESWIDEBANNERS_PATH, "TVSeries."+sStyle+".SeriesWideBanners.xml");
		case "1":
			SetXMLProperty(sFile, TVSERIES_FACADE_PATH, "TVSeries."+sStyle+".WideBannerMod.5x2.Facade.xml");
			SetXMLProperty(sFile, TVSERIES_SERIESWIDEBANNERS_PATH, "TVSeries.Common.WideBannerMod.5x2.SeriesWideBanners.xml");
		case "2":
			SetXMLProperty(sFile, TVSERIES_FACADE_PATH, "TVSeries."+sStyle+".WideBannerMod.5x3.Facade.xml");
			SetXMLProperty(sFile, TVSERIES_SERIESWIDEBANNERS_PATH, "TVSeries.Common.WideBannerMod.5x3.SeriesWideBanners.xml");
		case "3":
			SetXMLProperty(sFile, TVSERIES_FACADE_PATH, "TVSeries."+sStyle+".WideBannerMod.7x3.Facade.xml");
			SetXMLProperty(sFile, TVSERIES_SERIESWIDEBANNERS_PATH, "TVSeries.Common.WideBannerMod.7x3.SeriesWideBanners.xml");
		case "4":
			SetXMLProperty(sFile, TVSERIES_FACADE_PATH, "TVSeries."+sStyle+".WideBannerMod.10x4.Facade.xml");
			SetXMLProperty(sFile, TVSERIES_SERIESWIDEBANNERS_PATH, "TVSeries.Common.WideBannerMod.10x4.SeriesWideBanners.xml");
	endswitch;
	
	// Update MovingPictures Imports
	sFile = STREAMEDMP_SKIN_MOVINGPICTURES;
	
	sStyle = "default";
	if !g_bMovingPicturesDefault then
		sStyle = "fanart";
	endif;
		
	SetXMLProperty(sFile, MOVPICS_LISTVIEW_PATH, "movingpictures."+sStyle+".listview.xml");
	SetXMLProperty(sFile, MOVPICS_LISTVIEWMEDIAINFO_PATH, "movingpictures."+sStyle+".listview.mediainfo.xml");
	SetXMLProperty(sFile, MOVPICS_FILMSTRIPVIEW_PATH, "movingpictures."+sStyle+".filmstripview.xml");
	SetXMLProperty(sFile, MOVPICS_FILMSTRIPMEDIAINFO_PATH, "movingpictures."+sStyle+".filmstripview.mediainfo.xml");
	SetXMLProperty(sFile, MOVPICS_TOPBAR_PATH, "movingpictures."+sStyle+".topbar.xml");	
	
	switch (g_sThumbLayout)
		case "0":
			SetXMLProperty(sFile, MOVPICS_FACADE_PATH, "movingpictures."+sStyle+".facade.xml");			
			SetXMLProperty(sFile, MOVPICS_BACKGROUNDOVERLAY_PATH, "movingpictures."+sStyle+".background.overlays.xml");
			SetXMLProperty(sFile, MOVPICS_THUMBVIEW_PATH, "movingpictures."+sStyle+".thumbsview.xml");
			SetXMLProperty(sFile, MOVPICS_THUMBVIEWMEDIAINFO_PATH, "movingpictures."+sStyle+".thumbsview.mediainfo.xml");
		case "1":
			SetXMLProperty(sFile, MOVPICS_FACADE_PATH, "movingpictures."+sStyle+".thumbsview.12x4.8x3.facade.xml");			
			SetXMLProperty(sFile, MOVPICS_BACKGROUNDOVERLAY_PATH, "movingpictures."+sStyle+".thumbsview.12x4.8x3.background.overlays.xml");
			SetXMLProperty(sFile, MOVPICS_THUMBVIEW_PATH, "movingpictures.common.thumbsview.12x4.8x3.xml");
			SetXMLProperty(sFile, MOVPICS_THUMBVIEWMEDIAINFO_PATH, "movingpictures.common.thumbsview.12x4.8x3.mediainfo.xml");
		case "2":
			SetXMLProperty(sFile, MOVPICS_FACADE_PATH, "movingpictures."+sStyle+".thumbsview.12x3.8x2.facade.xml");			
			SetXMLProperty(sFile, MOVPICS_BACKGROUNDOVERLAY_PATH, "movingpictures."+sStyle+".thumbsview.12x3.8x2.background.overlays.xml");
			SetXMLProperty(sFile, MOVPICS_THUMBVIEW_PATH, "movingpictures.common.thumbsview.12x3.8x2.xml");
			SetXMLProperty(sFile, MOVPICS_THUMBVIEWMEDIAINFO_PATH, "movingpictures.common.thumbsview.12x3.8x2.mediainfo.xml");
	endswitch;
	
	// Update TV Mini Guide Imports
	sFile = STREAMEDMP_SKIN_TVMINIGUIDE;
	SetXMLProperty(sFile, TVMINIGUIDE_ROWS_PATH, "TVMiniGuide."+g_sTVMiniGuideSize+"Rows.xml");	
	
	// Update TV Guide Imports
	sFile = STREAMEDMP_SKIN_MYTVGUIDE;
	SetXMLProperty(sFile, TVGUIDE_ROWS_PATH, "mytvguide.common."+g_sTVGuideSize+"rows.channeltemplate.xml");
	
	sFile = STREAMEDMP_SKIN_DIALOGTVGUIDE;
	SetXMLProperty(sFile, TVGUIDE_ROWS_PATH, "mytvguide.common."+g_sTVGuideSize+"rows.channeltemplate.xml");
	
	sFile = STREAMEDMP_SKIN_4TRTVGUIDE;
	SetXMLProperty(sFile, TVGUIDE_ROWS_PATH, "mytvguide.common."+g_sTVGuideSize+"rows.channeltemplate.xml");
			
	// Enable/Disable Random Fanart in MyTV sections	
	szDefine = "#useRandomTVSeriesFanart";
	SetSkinDefine(STREAMEDMP_SKIN_4TRACTIVE, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_4TRPROGRAMINFO, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));		
	SetSkinDefine(STREAMEDMP_SKIN_4TRRECORDEDTV, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_4TRTVGUIDESEARCH, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_4TRUPCOMING, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_MYTVPROGRAM, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_MYTVRECORDEDINFO, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_MYTVRECORDEDTV, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_MYTVSCEDULERSERVER, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_MYTVSCEDULERSERVERSEARCH, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	SetSkinDefine(STREAMEDMP_SKIN_MYTVSEARCH, szDefine, BoolToString(g_bEnableRandomFanartInMyTV));	
	
	// Update Unfocused Alpha in references.xml
	// Valid values from 0 - 255 (Tranparent - Solid);
	sFile = STREAMEDMP_SKIN_REFERENCES;
	SetXMLProperty(sFile, REFERENCES_LISTALPHA_PATH, g_sUnfocusedAlphaListItems);	
	SetXMLProperty(sFile, REFERENCES_PLAYLISTALPHA_PATH, g_sUnfocusedAlphaListItems);
	SetXMLProperty(sFile, REFERENCES_THUMBALPHA_PATH, g_sUnfocusedAlphaThumbs);
	
	// Set List Facade Colors
	SetXMLProperty(sFile, REFERENCES_LISTCOLORTEXT_PATH, "FF"+g_sTextColor);
	SetXMLProperty(sFile, REFERENCES_LISTCOLORTEXT2_PATH, "FF"+g_sText2Color);
	SetXMLProperty(sFile, REFERENCES_LISTCOLORTEXT3_PATH, "FF"+g_sText3Color);
	SetXMLProperty(sFile, REFERENCES_LISTCOLORWATCHED_PATH, "FF"+g_sWatchedColor);
	SetXMLProperty(sFile, REFERENCES_LISTCOLORREMOTE_PATH, "FF"+g_sRemoteColor);
	
	SetXMLProperty(sFile, REFERENCES_PLAYLISTCOLORTEXT_PATH, "FF"+g_sTextColor);
	SetXMLProperty(sFile, REFERENCES_PLAYLISTCOLORTEXT2_PATH, "FF"+g_sText2Color);
	SetXMLProperty(sFile, REFERENCES_PLAYLISTCOLORTEXT3_PATH, "FF"+g_sText3Color);
	SetXMLProperty(sFile, REFERENCES_PLAYLISTCOLORWATCHED_PATH, "FF"+g_sWatchedColor);
	SetXMLProperty(sFile, REFERENCES_PLAYLISTCOLORREMOTE_PATH, "FF"+g_sRemoteColor);
	
	sFile = STREAMEDMP_SKIN_EMULATORS;
	SetXMLProperty(sFile, EMULATORS_SCREENSHOT, g_sThumbsDir^"myEmulators\\screenshots\\#selecteditem.jpg");	
	
	// Set Default Skin
	sFile = g_sConfigDir^"MediaPortal.xml";	
	if g_bSetAsDefaultSkin then
		SetMPXMLProperty(sFile, "skin","name",SKIN_NAME);
	else
		SetMPXMLProperty(sFile, "skin","name",DEFAULT_SKIN);		
	endif;             
	
	// Start with Basic Home or Standard Home	
	if g_bStartBasicHome then
		SetMPXMLProperty(sFile, "gui","startbasichome","yes");
	else
		SetMPXMLProperty(sFile, "gui","startbasichome","no");
	endif;
	
	// Set Trakt Settings
	SetTraktSettings();
	
	// Apply CheckSums
	ApplyCheckSum();	
	
end;             

function NUMBER	GetFileVersion(svVersionNumber,sType)
	STRING svValue,szFileName;	
begin
		
	switch (sType) 

        case "MEDIAPORTAL": 
            szFileName = g_sMediaPortalProgramDir^"MediaPortal.exe"; 

        case "TVSERIES": 
            szFileName = g_sPluginsDir^"windows\\MP-TVSeries.dll"; 

        case "MOVINGPICTURES": 
            szFileName = g_sPluginsDir^"windows\\MovingPictures.dll";
            
		case "LYRICS": 
            szFileName = g_sPluginsDir^"windows\\MyLyrics.dll";       
     
     	case "INFOSERVICE": 
            szFileName = g_sPluginsDir^"windows\\InfoService.dll"; 
            
        case "TRAILERS": 
            szFileName = g_sPluginsDir^"windows\\MyTrailers.dll"; 
            
        case "GLOBALSEARCH": 
            szFileName = g_sPluginsDir^"windows\\GUIGlobalSearch.dll"; 
            
        case "FANARTHANDLER": 
            szFileName = g_sPluginsDir^"process\\FanartHandler.dll";  
            
        case "TRAKT": 
            szFileName = g_sPluginsDir^"windows\\TraktPlugin.dll";    

    endswitch; 

    return VerGetFileVersion(szFileName,svVersionNumber);
    
end;

function VOID CheckMPVersion()
	STRING svVersion2, svVersion1;  
	NUMBER nResult;
begin
    
    // Warn user of old version of MediaPortal
    if (GetFileVersion(svVersion2,"MEDIAPORTAL") < 0) then    	
    	if (MODE!=SILENTMODE) then		
    		MessageBox(@MPNOTFOUND,SEVERE);
    	endif;
    	abort;                 	    	
    endif;
    
    // VerCompare Return Codes:
    // Return Code = 2 (Version1 = Version2)
    // Return Code = 1 (Version1 < Version2)
    // Return Code = 0 (Version1 > Version2)
    
    // Check Minimum requirements are met
    svVersion1 = MEDIAPORTAL_MIN_VERSION;
    if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
    	if (MODE!=SILENTMODE) then		
    		MessageBox(@MPMINREQNOTMET,SEVERE);
    	endif;
    	abort; 
    endif;
    
    // Check if warning should be prompted
    svVersion1 = MEDIAPORTAL_MINNOWARN_VERSION;
    if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
    	if (MODE!=SILENTMODE) then		
    		MessageBox(@MPOLDVERSION,WARNING);
    	endif;
    endif;        
    
end;

//function NUMBER GetMediaPortalRevision()
//	STRING svVersion;
//begin
//	GetFileVersion(svVersion,"MEDIAPORTAL");
//	return GetVersionDigit(svVersion, 4);	
//end;

/*function NUMBER GetVersionDigit(version, digit)
	STRING svString;
	LIST listVersionDigits;
	NUMBER nvVar;
begin

	// Check for valid index
	if (digit < 1 || digit > 4) then
		return -1;
	endif;
                                       
	listVersionDigits = ListCreate(STRINGLIST);
	                                       
    if StrGetTokens(listVersionDigits, version, ".") < 0 then
    	return -1;
    endif;
    
    if ListSetIndex(listVersionDigits, digit - 1) = 0 then
    	ListCurrentString(listVersionDigits, svString);
		StrToNum(nvVar, svString);
		return nvVar;
	else
		return -1;
	endif;
                          
end;*/

function NUMBER CheckPluginVersions(szQuestion)
	STRING svVersion1, svVersion2;
	STRING szMsg;
	NUMBER nReturn;
begin
    
    nReturn = 0;
    
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\TVSeries")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"TVSERIES") = 0 then
			svVersion1 = TVSERIES_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
	    		szQuestion = "Your current version of TV-Series plugin v" + svVersion2 + 
	    					" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
	    					svVersion1;
	    		nReturn=nReturn-1;
	    		
	    	endif;
	    endif;
    endif;
    
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\MovingPictures")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"MOVINGPICTURES") = 0 then
			svVersion1 = MOVINGPICTURES_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
		    	if nReturn < 0 then
		    		szQuestion = szQuestion + "\n\n";
		    	endif;
		    	szQuestion = szQuestion + "Your current version of Moving Pictures plugin v" + svVersion2 + 
		    				" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
		    				svVersion1;
		        nReturn = nReturn - 1;
	    	endif;
	    endif;
    endif; 
    
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\MyLyrics")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"LYRICS") = 0 then
			svVersion1 = MYLYRICS_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
		    	if nReturn < 0 then
		    		szQuestion = szQuestion + "\n\n";
		    	endif;
		    	szQuestion = szQuestion + "Your current version of My Lyrics plugin v" + svVersion2 + 
		    				" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
		    				svVersion1;
		        nReturn = nReturn - 1;
	    	endif;
	    endif;
    endif; 
        
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\InfoService")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"INFOSERVICE") = 0 then
			svVersion1 = INFOSERVICE_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
		    	if nReturn < 0 then
		    		szQuestion = szQuestion + "\n\n";
		    	endif;
		    	szQuestion = szQuestion + "Your current version of InfoService plugin v" + svVersion2 + 
		    				" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
		    				svVersion1;
		        nReturn = nReturn - 1;
	    	endif;
	    endif;
    endif;  
    
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\Trakt")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"TRAKT") = 0 then
			svVersion1 = TRAKT_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
		    	if nReturn < 0 then
		    		szQuestion = szQuestion + "\n\n";
		    	endif;
		    	szQuestion = szQuestion + "Your current version of Trakt plugin v" + svVersion2 + 
		    				" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
		    				svVersion1;
		        nReturn = nReturn - 1;
	    	endif;
	    endif;
    endif;
    
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\GlobalMusicSearch")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"GLOBALSEARCH") = 0 then
			svVersion1 = GLOBALSEARCH_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
		    	if nReturn < 0 then
		    		szQuestion = szQuestion + "\n\n";
		    	endif;
		    	szQuestion = szQuestion + "Your current version of Global Search plugin v" + svVersion2 + 
		    				" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
		    				svVersion1;
		        nReturn = nReturn - 1;
	    	endif;
	    endif;
    endif;    
    
    if (!FeatureIsItemSelected(MEDIA,"Plugins\\FanartHandler")) then
		// Check that compatible version of plugin is used
		if GetFileVersion(svVersion2,"FANARTHANDLER") = 0 then
			svVersion1 = FANARTHANDLER_MIN_VERSION;
	    	if VerCompare(svVersion1, svVersion2, VERSION) = 0 then
		    	if nReturn < 0 then
		    		szQuestion = szQuestion + "\n\n";
		    	endif;
		    	szQuestion = szQuestion + "Your current version of Fanart-Handler plugin v" + svVersion2 + 
		    				" is not supported by Streamed MP. Its recommended that you select this feature to upgrade to v" +
		    				svVersion1;
		        nReturn = nReturn - 1;
	    	endif;
	    endif;
    endif;

    if nReturn < 0 then
    	szQuestion = szQuestion + "\n\nDo you wish to go back to the select features dialog?";
    endif;
    
    return nReturn;
end;

function NUMBER LaunchBrowser(szSite)	
	OBJECT web;
begin
    
	try
		set web = CoCreateObjectDotNet(SUPPORTDIR^"Setup.dll", "Setup.Web" );
		web.LaunchBrowser(szSite);
		set web = NOTHING;
	catch
		//MessageBox("Unable to show website: " + szSite, INFORMATION);
	endcatch;
	
end;

function VOID MajorUpgradeCleanup()
	STRING szUninstallKey, svSubStr, svValue;
	NUMBER nvSize, nvType;
begin 
	// For some reason InstallShield doesnt cleanup files/settings properly
	// after upgrade, this function removes them manually

    // Check if old Uninstall Entry exists
   	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
   	
   	szUninstallKey = "SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Uninstall\\InstallShield_"+g_sMajorUpgradeGUID;
   	if (RegDBKeyExist(szUninstallKey) < 0) then
   		// Nothing to remove
   		return;
   	endif;
   	
   	// Get Path 
	RegDBGetKeyValueEx(szUninstallKey,"LogFile",nvType,svValue,nvSize);
	// Remove filename 'Setup.ilg' to path
	StrSub ( svSubStr, svValue, 0, (StrLength(svValue) - 9) );	
	
	// Remove leftover files/folder from previous install
	DeleteDir(svSubStr,ALLCONTENTS);
	
	RegDBGetKeyValueEx(szUninstallKey,"InstallSource",nvType,svValue,nvSize);
	DeleteDir(svValue,ALLCONTENTS);

    // Remove Uninstall entry in Add/Remove programs
    RegDBDeleteKey(szUninstallKey);
                       
end;

function VOID GetCachedInstallDir()
	STRING	szUninstallKey;
	NUMBER 	nvSize, nvType;
begin                           
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
    szUninstallKey = "SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Uninstall\\InstallShield_"+PRODUCT_GUID;
    RegDBGetKeyValueEx(szUninstallKey, "InstallSource", nvType, g_sDownloadInstallDir, nvSize);	
end; 

function VOID UninstallCleanup(bRemoveSkinSettings, bRemoveBasicHomeSettings)
	STRING szDir, sFile;
begin 
	// Remove cached downloaded install files                                           
	if (Is(PATH_EXISTS,g_sDownloadInstallDir)) then
    	DeleteDir(g_sDownloadInstallDir, ALLCONTENTS);    	
    endif;
       
    // Remove StreamedMPConfig.xml    
    if bRemoveSkinSettings then
    	DeleteFile(g_sConfigDir^"StreamedMPConfig.xml*");	
    endif;
    // Remove UserMenuProfile.xml    
    if bRemoveBasicHomeSettings then
    	DeleteFile(g_sConfigDir^"usermenuprofile.xml*");
    	DeleteFile(INSTALLDIR^SKIN_NAME^"BasicHome.xml*");
    endif;    
    
    // Remove any rouge XML files from skin directory
    szDir = INSTALLDIR^SKIN_NAME;
    DeleteFile(szDir^"*.xml");
    DeleteDir(szDir, ONLYDIR); 
    
    // Cleanup Virtual Store
    CleanVirtualStoreDir();
    
    // Finially set default skin back to DefaultWide
    sFile = g_sConfigDir^"MediaPortal.xml";    
    SetMPXMLProperty(sFile, "skin", "name", DEFAULT_SKIN);
        
end; 

function NUMBER SetMPXMLProperty(sFile, sSectionAttribute,sEntryAttribute, sValue)
	STRING sNode, svString;
	OBJECT oXMLdoc, oNodeListRoot, oNodeListElements;
	INT i, j;
begin
   	
   	//sFile = g_sConfigDir^"MediaPortal.xml";        	 
    Disable(LOGGING);
    
    try
	    // Prepare XML parser object
	    set oXMLdoc = CreateObject("Microsoft.XMLDOM");
	    
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
	    
	    // Load the document 	    
		oXMLdoc.Load (sFile);
		
		sNode = "/profile/section";	    
	    set oNodeListRoot = oXMLdoc.DocumentElement.SelectNodes(sNode); 	    	    
	    
	    // Traverse all section elements in xml until 
	    // we reach the the required configuration section    
	    for i=0 to oNodeListRoot.length - 1
		
			if oNodeListRoot(i).Attributes(0).Text = sSectionAttribute then                
                // retrieve the list of configurable elements for the section
            	set oNodeListElements = oNodeListRoot(i).childNodes;
            
            	for j = 0 to oNodeListElements.length - 1
                    
                    // we are only interested in the 'entry' elements
	                if oNodeListElements(j).baseName = "entry" then
	                    
	                    if oNodeListElements(j).Attributes(0).Text = sEntryAttribute then
	                		// set the MediaPortal property
	                        oNodeListElements(j).Text = sValue;	                       
	                    endif;
                
                	endif;	                               
                	
            	endfor;	       
            	
	        endif;
		
		endfor;   		    
	    
	    // save the xml	    
	    oXMLdoc.Save (sFile);
	    
	    // release objects used
	    set oXMLdoc = NOTHING;
	    set oNodeListRoot = NOTHING;
	    set oNodeListElements = NOTHING;	    	    
	    
	catch                  
		
		Enable(LOGGING);
	    return -1;    
	    
	endcatch;	       

    Enable(LOGGING);         
    return 0;
    
end;

function NUMBER GetMPXMLProperty(sFile, sSectionAttribute, sEntryAttribute, sValue)
	STRING sNode, svString;
	OBJECT oXMLdoc, oNodeListRoot, oNodeListElements;
	INT i, j;
	BOOL bEntryExists;
begin
    try
	    // Prepare XML parser object
	    set oXMLdoc = CreateObject("Microsoft.XMLDOM");
	    
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
	    
	    // Load the document 	    
		oXMLdoc.Load (sFile);
		
		sNode = "/profile/section";	    
	    set oNodeListRoot = oXMLdoc.DocumentElement.SelectNodes(sNode); 	    	    
	    
	    // Traverse all section elements in xml until 
	    // we reach the the required configuration section    
	    for i=0 to oNodeListRoot.length - 1
		
			if oNodeListRoot(i).Attributes(0).Text = sSectionAttribute then                
                // retrieve the list of configurable elements for the section
            	set oNodeListElements = oNodeListRoot(i).childNodes;
            
            	for j = 0 to oNodeListElements.length - 1
                    
                    // we are only interested in the 'entry' elements
	                if oNodeListElements(j).baseName = "entry" then
	                    
	                    if oNodeListElements(j).Attributes(0).Text = sEntryAttribute then
	                		// Get the MediaPortal property
	                        sValue = oNodeListElements(j).Text;
	                        bEntryExists = TRUE;	                       
	                    endif;
                
                	endif;	                               
                	
            	endfor;	       
            	
	        endif;
		
		endfor;   		    
	    	    
	    // release objects used
	    set oXMLdoc = NOTHING;
	    set oNodeListRoot = NOTHING;
	    set oNodeListElements = NOTHING;	    	    
	    
	catch                  
			
	    return -1;    
	    
	endcatch;	       
    
    if bEntryExists then
    	return 0;
    else
    	return -1;
    endif;
    
end;

/*function NUMBER GetXMLProperty(sFile, sNode, sPropertyValue)
	OBJECT oXMLdoc, oNode; 
begin
		
	try		
		// Prepare XML parser object
	    set oXMLdoc = CreateObject("Microsoft.XMLDOM");
	    
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
	    
	    // Load the document 	    
		oXMLdoc.Load (sFile);	
	    set oNode = oXMLdoc.DocumentElement.SelectSingleNode(sNode);
	    
	    // return property
	    sPropertyValue = oNode.Text;
					 	
	    // release objects used
	    set oXMLdoc = NOTHING;	    
	    set oNode = NOTHING;  	    
	    
	catch                  
		
	    return -1;    
	    
	endcatch;	       
  
    return 0;
    
end; */

function NUMBER SetXMLProperty(sFile, sNode, sPropertyValue)
	OBJECT oXMLdoc, oNode; 
begin
	
	Disable(LOGGING);
		
	try		
		// Prepare XML parser object
	    set oXMLdoc = CreateObject("Microsoft.XMLDOM");
	    
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
	    
	    // Load the document 	    
		oXMLdoc.Load (sFile);	
	    set oNode = oXMLdoc.DocumentElement.SelectSingleNode(sNode);
	    
	    // Set property
	    oNode.Text = sPropertyValue;
		
		// Save XML
		oXMLdoc.Save (sFile);		
		
	    // release objects used
	    set oXMLdoc = NOTHING;	    
	    set oNode = NOTHING;  	    
	    
	catch                  
		
		Enable(LOGGING);
	    return -1;    
	    
	endcatch;	       

    Enable(LOGGING);         
    return 0;
    
end;
       
function VOID CleanVirtualStoreDir()
	STRING szValue, szVirtualStore;
	NUMBER nResult;
begin    
    // Remove Vista's Virtual Store directory for the StreamedMP Skin
    // We do this just incase a user with UAC enabled manually updated settings!    
    Disable(LOGGING);
    
 	try   
		nResult = SHFolder.SHGetFolderPathA(NULL, CSIDL_PROFILE, NULL, 0, szValue);
	    if nResult = 0 then
	    	szVirtualStore = szValue ^ "AppData\\Local\\VirtualStore\\Program Files\\Team MediaPortal\\MediaPortal\\Skin\\StreamedMP";
	    	if Is(PATH_EXISTS,szVirtualStore) then
	    		DeleteDir(szVirtualStore,ALLCONTENTS);
	    	endif;   
	    	szVirtualStore = szValue ^ "AppData\\Local\\VirtualStore\\Program Files (x86)\\Team MediaPortal\\MediaPortal\\Skin\\StreamedMP";
	    	if Is(PATH_EXISTS,szVirtualStore) then
	    		DeleteDir(szVirtualStore,ALLCONTENTS);
	    	endif;
	    	szVirtualStore = szValue ^ "AppData\\Local\\VirtualStore\\ProgramData\\Team MediaPortal\\MediaPortal\\Cache\\StreamedMP";
	    	if Is(PATH_EXISTS,szVirtualStore) then
	    		DeleteDir(szVirtualStore,ALLCONTENTS);
	    	endif;
	    endif;	    
	catch
		Enable(LOGGING);
		return;
	endcatch;	
	
	Enable(LOGGING);
	
end;  

/*function VOID EnableControl(szDialogName, nControl, bState)
	HWND hwndDlg;	
begin
	hwndDlg = CmdGetHwndDlg( szDialogName );
	EnableWindow( GetDlgItem( hwndDlg, nControl), bState);	
end;*/

function NUMBER SetSkinDefine(szFile, szDefine, szValue)
	OBJECT oXMLdoc, oNodes;
	STRING sNode;
	INT i;
begin
	Disable(LOGGING);
	
	try
		// Prepare XML parser object
		set oXMLdoc = CreateObject("Microsoft.XMLDOM");
		
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
		
		// Load the document 	    
		oXMLdoc.Load (szFile);
		
		sNode = "/window/define";	    
	    set oNodes = oXMLdoc.DocumentElement.SelectNodes(sNode);
		
		// Traverse all defines in xml until 
	    // we reach 'szDefine'
	    for i=0 to oNodes.length - 1
		
			if oNodes(i).Text % szDefine then                
                // Match found
                oNodes(i).Text = szDefine + ":" + szValue;
	        endif;
		
		endfor;   		    
	    
	    // save the xml	    
	    oXMLdoc.Save (szFile);
	    
	    // release objects used
	    set oXMLdoc = NOTHING;
	    set oNodes = NOTHING;	   
	    
	catch
	    
	    Enable(LOGGING);
		return -1;
		
	endcatch;
	
	Enable(LOGGING);
	return 0;

end;

/*function NUMBER GetSkinDefine(szFile, szDefine, szValue)
	OBJECT oXMLdoc, oNodes;
	STRING sNode;
	INT i;
begin
	Disable(LOGGING);
	
	try
		// Prepare XML parser object
		set oXMLdoc = CreateObject("Microsoft.XMLDOM");
		
		oXMLdoc.async = FALSE;
		oXMLdoc.preserveWhiteSpace = TRUE;
		
		// Load the document 	    
		oXMLdoc.Load (szFile);
		
		sNode = "/window/define";	    
	    set oNodes = oXMLdoc.DocumentElement.SelectNodes(sNode);
		
		// Traverse all defines in xml until 
	    // we reach 'szDefine'
	    for i=0 to oNodes.length - 1
		
			if oNodes(i).Text % szDefine then                
                // Match found, get value                  
                StrSub ( szValue, oNodes(i).Text, (StrLength(szDefine) + 1), 3 );                
	        endif;
		
		endfor;   		    
	
	    // release objects used
	    set oXMLdoc = NOTHING;
	    set oNodes = NOTHING;	   
	    
	catch
	    
	    Enable(LOGGING);
		return -1;
		
	endcatch;
	
	Enable(LOGGING);
	return 0;

end;  */
    
function NUMBER ReplaceLineInFile(szFileName, szSearchStr, szReplaceStr)
	NUMBER nResult, nIndex;
	STRING svString;
	LIST listID;	
begin    
 	
 	Disable(LOGGING);

	listID = ListCreate(STRINGLIST);
	ListReadFromFile ( listID, szFileName );
	
	nResult = ListGetFirstString(listID, svString);
	
	while (nResult != END_OF_LIST)		
		if (svString % szSearchStr) then		
			// Replace Line
			ListSetCurrentString ( listID, szReplaceStr );			
		endif;
		nResult = ListGetNextString(listID, svString);		
	endwhile;
	
	// Delete old file
	DeleteFile(szFileName);
	// Create new file
	ListWriteToFile(listID,szFileName);
	
	ListDestroy(listID);
	
	Enable(LOGGING);
	return 0;
	   
end;
  
function BOOL AdjustForScreenSize()
	NUMBER nvDx, nvDy;
	STRING sValue, sFile;
begin
	
	// Get Screen Dimmensions
	if GetExtents ( nvDx, nvDy ) < 0 then
		return FALSE;
	endif;
	
	// Dont Adjust if not 1920 wide, we dont want to support any other resolution at this time
	if nvDx != 1920 then
		return FALSE;	
	endif;
	
	// Check if MediaPortal is Running in Full Screen
	// If not then it will run in Windowed Mode using default resolution 
	sFile = g_sConfigDir^"MediaPortal.xml";
	GetMPXMLProperty(sFile, "general", "startfullscreen", sValue);
	if sValue = "yes" then
		return TRUE;
	else
		return FALSE;
	endif;	
	
end; 

// This function will modify skin files that runing 1920x1080 screen resolution
// and are also running MediaPortal in Fullscreen
function VOID ModifySkinFilesForFULLHD(b1920)
	STRING szFileName, szSearchStr, szReplaceStr;
	STRING szDefine, szValue;
begin
    
    // Adjust TVSeries Watched / Unwatched string
    // Using 1920 wide the watched value is displaced further from the watched icon...dont ask me why!    
    szFileName = STREAMEDMP_SKIN_TVSERIESSETTINGS;
    
    // String replacement will only occur if a match ie. currently has the wrong value
    if b1920 then
	    // Needs adjustments by 2 pixls to the left
	    szSearchStr = "<Enabled>1<Format>SeriesWatchedAndUnWatched<FormatAs>Eval(PrettyNumber100(<Series.EpisodesUnWatched>))         Eval(PrettyNumber100(<Series.EpisodeCount>-<Series.EpisodesUnWatched>))";
	    szReplaceStr = "<Enabled>1<Format>SeriesWatchedAndUnWatched<FormatAs>Eval(PrettyNumber100(<Series.EpisodesUnWatched>))       Eval(PrettyNumber100(<Series.EpisodeCount>-<Series.EpisodesUnWatched>))";    
	    ReplaceLineInFile(szFileName, szSearchStr, szReplaceStr);
	    
	    szSearchStr = "<Enabled>1<Format>SeasonWatchedAndUnWatched<FormatAs>Eval(PrettyNumber10(<Season.EpisodesUnWatched>))          Eval(PrettyNumber10(<Season.EpisodeCount>-<Season.EpisodesUnWatched>))";
	    szReplaceStr = "<Enabled>1<Format>SeasonWatchedAndUnWatched<FormatAs>Eval(PrettyNumber10(<Season.EpisodesUnWatched>))         Eval(PrettyNumber10(<Season.EpisodeCount>-<Season.EpisodesUnWatched>))";    
	    ReplaceLineInFile(szFileName, szSearchStr, szReplaceStr);
	else        
	    szSearchStr = "<Enabled>1<Format>SeriesWatchedAndUnWatched<FormatAs>Eval(PrettyNumber100(<Series.EpisodesUnWatched>))       Eval(PrettyNumber100(<Series.EpisodeCount>-<Series.EpisodesUnWatched>))";
	    szReplaceStr = "<Enabled>1<Format>SeriesWatchedAndUnWatched<FormatAs>Eval(PrettyNumber100(<Series.EpisodesUnWatched>))         Eval(PrettyNumber100(<Series.EpisodeCount>-<Series.EpisodesUnWatched>))";    
	    ReplaceLineInFile(szFileName, szSearchStr, szReplaceStr);
	    
	    szSearchStr = "<Enabled>1<Format>SeasonWatchedAndUnWatched<FormatAs>Eval(PrettyNumber10(<Season.EpisodesUnWatched>))         Eval(PrettyNumber10(<Season.EpisodeCount>-<Season.EpisodesUnWatched>))";
	    szReplaceStr = "<Enabled>1<Format>SeasonWatchedAndUnWatched<FormatAs>Eval(PrettyNumber10(<Season.EpisodesUnWatched>))          Eval(PrettyNumber10(<Season.EpisodeCount>-<Season.EpisodesUnWatched>))";    
	    ReplaceLineInFile(szFileName, szSearchStr, szReplaceStr);    
    endif; 
   
end; 

function void DetectWinLargeFonts()
	STRING szKey, svValue, szMessage;
	NUMBER nvType, nvSize, nvValue;
begin
	if (MODE=SILENTMODE) then
		return;
	endif;
	
	// Detect Windows Font Size
	RegDBSetDefaultRoot(HKEY_CURRENT_USER);
	
	szKey = "Control Panel\\Desktop\\WindowMetrics";
	if RegDBGetKeyValueEx(szKey,"AppliedDPI", nvType, svValue, nvSize) < 0 then
		svValue = "96";
	endif;
	StrToNum(nvValue, svValue);
	
	// StreamedMP does not support Windows Fonts > 96dpi
	if nvValue > 96 then
			
		szMessage = "WARNING: Large Fonts setting has been detected in windows,\n";
		szMessage = szMessage + "this is not supported by StreamedMP. We recommend you set\n";
		szMessage = szMessage + "your font setting back to Default before starting\n";
		szMessage = szMessage + "MediaPortal.";
		
		MessageBox(szMessage,WARNING);
		
	endif;
end;

function string BoolToString(bState)
	STRING sReturn;
begin
    if bState then
    	sReturn = "Yes";
    else
    	sReturn = "No";
    endif;
    return sReturn;
end; 

function string BoolToStringInt(bState)
	STRING sReturn;
begin
    if bState then
    	sReturn = "1";
    else
    	sReturn = "0";
    endif;
    return sReturn;
end; 

function VOID GenerateBasicHome()
	STRING szFile;
begin
	szFile = g_sMediaPortalProgramDir ^ "SMPEditor.exe";
	if (Is(FILE_EXISTS, szFile)) then
		LaunchAppAndWait(szFile, "/regenerateonly", TRUE);
	endif;	
end;        

function VOID ReadTVLogoDefaults()
	STRING sFile,svValue;
begin    
	sFile = g_sConfigDir^"StreamedMPConfig.xml";
	if GetMPXMLProperty(sFile, "InstallFeatures", "tvLogosAustralia", svValue) = 0 then
		if svValue = "1" then
			FeatureSelectItem ( MEDIA, TVLOGOS_AUSTRALIA, TRUE );	
		endif;
	endif;
	
	if GetMPXMLProperty(sFile, "InstallFeatures", "tvLogosGermanyClassical", svValue) = 0 then
		if svValue = "1" then
			FeatureSelectItem ( MEDIA, TVLOGOS_GERMANY_CLASSIC, TRUE );	
		endif;
	endif;	
	
	if GetMPXMLProperty(sFile, "InstallFeatures", "tvLogosGermanyModern", svValue) = 0 then
		if svValue = "1" then
			FeatureSelectItem ( MEDIA, TVLOGOS_GERMANY_MODERN, TRUE );	
		endif;
	endif;	
	
	if GetMPXMLProperty(sFile, "InstallFeatures", "tvLogosGermanyTransparent", svValue) = 0 then
		if svValue = "1" then
			FeatureSelectItem ( MEDIA, TVLOGOS_GERMANY_TRANSPARENT, TRUE );	
		endif;
	endif;
	
	if GetMPXMLProperty(sFile, "InstallFeatures", "tvLogosCanada", svValue) = 0 then
		if svValue = "1" then
			FeatureSelectItem ( MEDIA, TVLOGOS_CANADA, TRUE );	
		endif;
	endif;
	
	if GetMPXMLProperty(sFile, "InstallFeatures", "tvLogosUK", svValue) = 0 then
		if svValue = "1" then
			FeatureSelectItem ( MEDIA, TVLOGOS_UK, TRUE );	
		endif;
	endif;	
end;

function VOID SaveTVLogoSelections()

begin

	g_sTVLogosAustralia = "0";
	g_sTVLogosGermanyClassical = "0";
	g_sTVLogosGermanyModern = "0";
	g_sTVLogosGermanyTransparent = "0";
	g_sTVLogosCanada = "0";
	g_sTVLogosUK = "0";

    if FeatureIsItemSelected( MEDIA, TVLOGOS_AUSTRALIA) then
    	g_sTVLogosAustralia = "1";
    endif;
    
    if FeatureIsItemSelected( MEDIA, TVLOGOS_GERMANY_CLASSIC) then
    	g_sTVLogosGermanyClassical = "1";
    endif; 
    
    if FeatureIsItemSelected( MEDIA, TVLOGOS_GERMANY_MODERN) then
    	g_sTVLogosGermanyModern = "1";
    endif;
    
    if FeatureIsItemSelected( MEDIA, TVLOGOS_GERMANY_TRANSPARENT) then
    	g_sTVLogosGermanyTransparent = "1";
    endif;
    
    if FeatureIsItemSelected( MEDIA, TVLOGOS_CANADA) then
    	g_sTVLogosCanada = "1";
    endif;
    
    if FeatureIsItemSelected( MEDIA, TVLOGOS_UK) then
    	g_sTVLogosUK = "1";
    endif;    
    
end;

function void GetTraktSettings()
	OBJECT xmlReader, db;
	STRING sFile, sResult;	
begin
	try
		set xmlReader = CoCreateObjectDotNet(SUPPORTDIR^"Setup.dll", "MediaPortal.XmlReader" );
		
		sFile = g_sConfigDir ^ "MediaPortal.xml";
		
		// Load MediaPortal XML file
		xmlReader.Load(sFile);
		
		// Read Settings
		g_sTraktUserName = xmlReader.GetEntry("Trakt", XML_TRAKT_USERNAME);
		g_sTraktPassword = xmlReader.GetEntry("Trakt", XML_TRAKT_PASSWORD);
		
		sResult = xmlReader.GetEntry("Trakt", XML_TRAKT_TVSERIES);
		if (sResult != "") then
			g_sTraktTVSeries = sResult;
		endif;
		
		sResult = xmlReader.GetEntry("Trakt", XML_TRAKT_MOVPICS);
		if (sResult != "") then
			g_sTraktMovingPictures = sResult;
		endif; 
		
		sResult = xmlReader.GetEntry("Trakt", XML_TRAKT_MYVIDEOS);
		if (sResult != "") then
			g_sTraktMyVideos = sResult;
		endif;
		
		sResult = xmlReader.GetEntry("Trakt", XML_TRAKT_MYFILMS);
		if (sResult != "") then
			g_sTraktMyFilms = sResult;
		endif;
						
		if (g_sTraktUserName = "" && g_sTraktPassword = "") then 
			
			// Try load setting from TVSeries database
			set db = CoCreateObjectDotNet(SUPPORTDIR^"Setup.dll", "TVSeries.Database" );
			
			sFile = g_sDatabaseDir ^ "TVSeriesDatabase4.db3";			 
			if db.Open(sFile) then		
				// Upgrade settings from tvseries database
				g_sTraktUserName = db.GetOption("TraktUsername");
				g_sTraktPassword = db.GetOption("TraktPassword");
				
				// Clear settings in TVSeries database now that we have upgraded
				db.SetOption("TraktUsername", "");
				db.SetOption("TraktPassword", "");
			endif;	
						 
			set db = NOTHING;
			 			 
		endif;
				
		// Destroy Object
	    set xmlReader = NOTHING;
	catch
		if (MODE != SILENTMODE) then
	 		//MessageBox("Error reading Trakt settings!",INFORMATION);
	 	endif;
	endcatch;	      
end;

function void SetTraktSettings()
	OBJECT xmlWriter;
	STRING sFile;
begin        
	
	sFile = g_sConfigDir ^ "MediaPortal.xml";
    
    try 
	    set xmlWriter = CoCreateObjectDotNet(SUPPORTDIR^"Setup.dll", "MediaPortal.XmlWriter" );
	    
	    // check if MediaPortal.xml exists, otherwise create it
	    // this could happen if we install directly after 
	    // MediaPortal without loading and saving configuration
	    if (!Is(FILE_EXISTS, sFile)) then
	    	// Create it
	    	xmlWriter.CreateXmlConfigFile(sFile);
	    endif;
       
       	// Load Document
       	xmlWriter.Load(sFile);
         
        xmlWriter.SetEntry("Trakt", XML_TRAKT_USERNAME, g_sTraktUserName);
        xmlWriter.SetEntry("Trakt", XML_TRAKT_PASSWORD, g_sTraktPassword);
        xmlWriter.SetEntry("Trakt", XML_TRAKT_TVSERIES, g_sTraktTVSeries);  
        xmlWriter.SetEntry("Trakt", XML_TRAKT_MOVPICS, g_sTraktMovingPictures);  
        xmlWriter.SetEntry("Trakt", XML_TRAKT_MYVIDEOS, g_sTraktMyVideos);    
        xmlWriter.SetEntry("Trakt", XML_TRAKT_MYFILMS, g_sTraktMyFilms);
        
        // Enable plugin on mediaportal startup
        if (Is(FILE_EXISTS, g_sPluginsDir ^ "windows\\TraktPlugin.dll")) then
        	xmlWriter.SetEntry("plugins", "Trakt", "yes");
        	xmlWriter.SetEntry("pluginsdlls", "TraktPlugin.dll", "yes");
        endif;
        
        // Save Document
        xmlWriter.Save(sFile);
           
     	set xmlWriter = NOTHING;
     catch
     	if (MODE != SILENTMODE) then
     		//MessageBox("Error writing Trakt settings!",INFORMATION);
     	endif;
     endcatch;
    
end;
